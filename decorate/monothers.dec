//Decorate data base for other monsters.

// --->
// SkeletonArcher
// --->
actor SkeletonArcher
{
	Tag "Skeleton Archer"
	Health 90
	Radius 20
	Height 56
	Speed 10
	mass 600 // Why?
	PainChance 15 // There's no pain state!
	Species "Undead"
	DropItem "LifeEssence" 75
	DropItem "ArmorBonusMax" 75
	MONSTER 
	+FLOORCLIP
	+NOBLOOD
	+DONTHARMSPECIES
	+PAINLESS
	DeathSound "skeldie"
	Obituary "%o was sniped by a skeleton archer."
	Scale 0.3
	States
	{
	Spawn:
		SKAR A 20 A_Look
		Loop
	See:
		SKAR B 0 A_ChangeFlag("FRIGHTENED", 0)
		SKAR B 0 A_ChangeFlag("MISSILEMORE", 1)
		SKAR B 0 A_PlaySound("skelwalk")
		SKAR B 0 A_JumpIfCloser(600, "Getaway")
		SKAR BCDE 4 A_Chase
		SKAR F 0 A_PlaySound("skelwalk")
		SKAR F 0 A_JumpIfCloser(600, "Getaway")
		SKAR FGHI 4 A_Chase
		Loop
	Getaway:
		SKAR B 0 A_ChangeFlag("FRIGHTENED", 1)
		SKAR B 0 A_ChangeFlag("MISSILEMORE", 0)
		SKAR B 0 A_PlaySound("skelwalk")
		SKAR BCDE 3 A_Chase
		SKAR F 0 A_PlaySound("skelwalk")
		SKAR FGHI 3 A_Chase
		SKAR F 0 A_JumpIfCloser(600, "Getaway")
		SKAR F 0 A_ChangeFlag("FRIGHTENED", 0)
		SKAR B 0 A_ChangeFlag("MISSILEMORE", 1)
		Goto Missile
	Missile:
		SKAR JKL 3 A_FaceTarget
		SKAR M 5
		SKAR N 5 A_PlaySound("skelaim")
		SKAR OOOOOOOOOO 2 A_FaceTarget
		SKAR P 4 A_CustomMissile("ArrowFire",42,0,0)
		SKAR Q 4
		Goto See
	Death:
		SKAR R 3
		SKAR S 3 A_Scream
		SKAR XYZ 3
		SKAD A 3 A_Fall
		SKAD B -1
		Stop
	}
}

ACTOR ArrowFire
{ 
	Radius 3 
	Height 2 
	Speed 50
	Scale 0.5
	Damage 5
	PROJECTILE 
	+NOGRAVITY 
	+GHOST
	+WINDTHRUST
	+NOEXTREMEDEATH
	SeeSound "skelbow"
	DeathSound "skelbowhit"
	States 
	{ 
	Spawn: 
		ARRW AAAAAAAAAAAAAAA 2 A_SpawnItem("BulletPuff") 
		ARRW A 0 A_Changeflag("NOGRAVITY", 0)
		ARRW A 0 A_Changeflag("LOWGRAVITY", 1)
		Loop
	Death: 
		ARRW A 0 A_Changeflag("NOGRAVITY", 0)
		ARRW A 0 A_Changeflag("LOWGRAVITY", 0)
		ARRW A 15
		Stop 
	} 
}

// --->
// Brute
// --->
ACTOR Brute
{
	Tag "Brute"
	Health 280
	Radius 32
	Height 72
	mass 2000
	Speed 12
	PainChance 5
	MONSTER
	+DROPOFF
//	+DONTTHRUST
	+NOICEDEATH
	DropItem "LifeEssence" 75
	DropItem "ArmorBonusMax" 75
	PainSound "BrutePain"
	DeathSound "BruteDie"
	ActiveSound "grunt/active"
	Obituary "%o was nuked by a Brute."
	DamageType "Explosion"
	DamageFactor "Explosion", 0.3
	States
	{
	Spawn:
		BRUT AB 10 A_Look
		Loop
	See:
		BRUT AA 3 A_Chase
		BRUT A 0 A_JumpIfCloser(240,"Explode")
		BRUT BB 3 A_Chase
		BRUT A 0 A_JumpIfCloser(240,"Explode")
		BRUT A 0 A_PlaySound("Brutewalk1")
		BRUT CC 3 A_Chase
		BRUT A 0 A_JumpIfCloser(240,"Explode")
		BRUT DD 3 A_Chase
		BRUT A 0 A_JumpIfCloser(240,"Explode")
		BRUT A 0 A_PlaySound("Brutewalk2")
		Loop
	Pain:
		BRUT I 0 A_Pain
		BRUT III 4 A_Chase
		Goto See
	Death:
		BRUT J 20 A_Scream
		BRUT K 20
		BRUT K 0 A_Explode(64, 512, 1)
		BRUT K 0 A_Jump (256, 2)
	Explode:
		BRUT K 0 A_Die
		BRUT K 0 A_Explode(128, 512, 1)
		BRUT K 0 A_SpawnItemEx("Brutenukefxspawner", 0, 0, 0, 0, 0, 0, 0, 128, 0)
		BRUT K 0 A_Fall
		BRUT L 10
		BRUT M 10
		BRUT N -1
		Stop
	}
}

ACTOR Brutenukefxspawner
{
	-SOLID
	+NOGRAVITY
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("Bruteexplode")
		TNT1 AAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("Brutefx1",35,0,random(0,360), 2, 80)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("Brutefx2",35,0,random(0,360), 2, random(-25,25))
		MISL BCD 7
		Stop
	}
}

ACTOR Brutefx1
{
	-SOLID
	PROJECTILE
	+DOOMBOUNCE
	-NOGRAVITY
	Damage 0
	radius 1
	height 1
	Speed 22
	States
	{
	Spawn:
		MISL BCD 15
		Stop
	}
}

ACTOR Brutefx2 : Brutefx1
{
	+NOGRAVITY
	Speed 30
	States
	{
	Spawn:
		MISL BCD 5
		Stop
	}
}

// --->
// GolemRider
// --->
actor GolemRider
{
	Tag "Golem Rider"
	Health 1000
	Radius 20
	Height 80
	Speed 6
	Scale 0.5
	MONSTER 
	+FLOORCLIP
	+MISSILEEVENMORE
	+NOBLOOD
	+BOSS
	+THRUGHOST
	+MISSILEMORE
	+DONTHARMSPECIES
	+DROPOFF
//	+DONTTHRUST
	mass 10000
	Obituary "%o was crushed by golem rider."
	MeleeDamage 13
	maxstepheight 64
	Species "Rock"
	DropItem "LifeEssence" 125
	DropItem "ArmorBonusMax" 125
	States
	{
	Spawn:
		BOTA A 4 A_Look
		Loop
	See:
		BOTA B 0 A_TakeInventory("GolemRiderToken",8)
		BOTA BBCCDDEEFFHHAA 2 A_Chase
		Loop
	Missile:
		BOTA A 0 A_FaceTarget
		BOTA A 0 A_PlaySound("bullchase")
		BOTA A 2 A_Recoil(-4)
		BOTA A 0 A_FaceTarget
		BOTA A 0 A_JumpIfCloser(256, "Charge")
		BOTA B 2 A_Recoil(-4)
		BOTA B 0 A_FaceTarget
		BOTA B 0 A_JumpIfCloser(256, "Charge")
		BOTA C 2 A_Recoil(-4)
		BOTA C 0 A_FaceTarget
		BOTA C 0 A_JumpIfCloser(256, "Charge")
		BOTA D 2 A_Recoil(-4)
		BOTA D 0 A_FaceTarget
		BOTA D 0 A_JumpIfCloser(256, "Charge")
		BOTA E 2 A_Recoil(-4)
		BOTA E 0 A_FaceTarget
		BOTA E 0 A_JumpIfCloser(256, "Charge")
		BOTA F 2 A_Recoil(-4)
		BOTA F 0 A_FaceTarget
		BOTA F 0 A_JumpIfCloser(256, "Charge")
		BOTA H 2 A_Recoil(-4)
		BOTA H 0 A_FaceTarget
		BOTA H 0 A_JumpIfCloser(256, "Charge")
		BOTA A 2 A_Recoil(-4)
		BOTA A 0 A_FaceTarget
		BOTA A 0 A_JumpIfCloser(256, "Charge")
		BOTA B 2 A_Recoil(-4)
		BOTA B 0 A_FaceTarget
		BOTA B 0 A_JumpIfCloser(256, "Charge")
		BOTA C 2 A_Recoil(-4)
		BOTA C 0 A_FaceTarget
		BOTA C 0 A_JumpIfCloser(256, "Charge")
		BOTA D 2 A_Recoil(-4)
		BOTA D 0 A_FaceTarget
		BOTA D 0 A_JumpIfCloser(256, "Charge")
		BOTA E 2 A_Recoil(-4)
		BOTA E 0 A_FaceTarget
		BOTA E 0 A_JumpIfCloser(256, "Charge")
		BOTA F 2 A_Recoil(-4)
		BOTA F 0 A_FaceTarget
		BOTA F 0 A_JumpIfCloser(256, "Charge")
		BOTA H 2 A_Recoil(-4)
		BOTA H 0 A_FaceTarget
		BOTA H 0 A_JumpIfCloser(256, "Charge")	
		BOTA A 0 A_GiveInventory("GolemRiderToken", 1)
		BOTA A 0 A_JumpIfInventory("GolemRiderToken", 5, "See")
		Goto Missile
	Charge:
		BOTA A 0 A_PlaySound("bullchase")
		BOTA A 0 A_FaceTarget
		BOTA A 2 A_Recoil(-4)
		BOTA B 0 A_SpawnItem("Hornattack", 30, 5)
		BOTA B 2 A_Recoil(-4)
		BOTA C 0 A_SpawnItem("Hornattack", 30, 5)
		BOTA C 2 A_Recoil(-4)
		BOTA D 0 A_SpawnItem("Hornattack", 30, 5)
		BOTA D 2 A_Recoil(-4)
		BOTA E 0 A_SpawnItem("Hornattack", 30, 5)
		BOTA E 2 A_Recoil(-4)
		BOTA F 0 A_SpawnItem("Hornattack", 30, 5)
		BOTA F 2 A_Recoil(-4)
		BOTA G 0 A_SpawnItem("Hornattack", 30, 5)
		BOTA G 2 A_Recoil(-4)
		BOTA H 0 A_SpawnItem("Hornattack", 30, 5)
		BOTA H 2 A_Recoil(-4)
		BOTA A 0 A_SpawnItem("Hornattack", 30, 5)
		BOTA A 2 A_Recoil(-4)
		BOTA B 0 A_SpawnItem("Hornattack", 30, 5)
		BOTA B 2 A_Recoil(-4)
		BOTA C 0 A_SpawnItem("Hornattack", 30, 5)
		BOTA C 2 A_Recoil(-4)
		BOTA D 0 A_SpawnItem("Hornattack", 30, 5)
		BOTA D 2 A_Recoil(-4)
		BOTA E 0 A_SpawnItem("Hornattack", 30, 5)
		BOTA E 2 A_Recoil(-4)
		BOTA F 0 A_SpawnItem("Hornattack", 30, 5)
		BOTA F 2 A_Recoil(-4)
		BOTA G 0 A_SpawnItem("Hornattack", 30, 5)
		BOTA G 2 A_Recoil(-4)
		BOTA H 0 A_SpawnItem("Hornattack", 30, 5)
		BOTA H 2 A_Recoil(-4)
		BOTA A 0 A_SpawnItem("Hornattack", 30, 5)
		BOTA A 6 A_Stop
		Goto See
	Death:
		BOTA A 2 A_Fall
		BOTA S 5 A_PlaySound("riderdie")
		BOTA T 10
		BOTA U 30
		BOTA W 9 A_PlaySound("ridersmash")
		BOTA XY 9
		BOTA Z 200
		Goto Fading
	Fading:
		BOTA Z 2 A_FadeOut(0.1)
		Loop
	}
}

Actor GolemRiderToken : Inventory { Inventory.Amount 1 Inventory.MaxAmount 8 }

actor Hornattack
{
	-SOLID
	+LOOKALLAROUND
	speed 0
	radius 80
	Height 40
	Damagetype "Rock"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Explode(20, 60, 0)
		TNT1 AAA 1 A_LookEx(2, 0, 64, 0, 0, "See")
		Stop
	See:
		TNT1 A 0 A_Explode(20, 60, 0)
		TNT1 AAA 1 A_Chase
		Stop
	Melee:
		TNT1 A 1 A_GiveToTarget("Hornattackinventory",0)
		Stop
	}
}

actor Hornattackinventory : CustomInventory
{
	-COUNTITEM
	states
	{
	Spawn:
		TNT1 A -1
		stop
	Pickup:
		TNT1 A 0 ThrustThingZ(0,random(30,45),0,1)
		stop
	}
}

// --->
// Anubis - Give credit to cutman and bouncy blah blah DAMMIT WARIO I DONT FEEL LIKE EDITING
// --->
actor Anubis
{
	Tag "Anubis"
	Height 120
	Radius 30
	Scale 0.37
	MONSTER
	renderstyle translucent
	alpha 1.0
	Speed 5
	PainChance 2
	mass 5000
	activesound "anubisact"
	seesound "anubissee"
	painsound "anubispain"
	meleerange 60
	health 700
	obituary "%o was blasted by an Anubis minister."
	hitobituary "%o was whacked by an Anubis minister."
	BloodType "BlackBlood"
	BloodColor "30 30 30"
	Species "Undead"
	+DONTHARMSPECIES
	DropItem "LifeEssence" 105
	DropItem "ArmorBonusMax" 105
	States
	{
	Spawn:
	ANBS A 1 A_Look
	Loop
	See:
	ANBS B 0 A_PlaySoundEx("anubisstep", "Weapon")
	ANBS BBCCDD 2 A_Chase
	ANBS E 0 A_PlaySoundEx("anubisstep", "Weapon")
	ANBS EEFFGG 2 A_Chase
	Loop
	Missile:
	ANBS MLKJ 4 A_FaceTarget
	ANBS I 0 A_SpawnItem("Greenportal", 0, 165)
	ANBS IIIIIIIIII 4 A_FaceTarget
	ANBS I 0 A_CustomMissile("AnubisTracer", 165,0,0)
	ANBS JKLM 5
	Goto See
	Pain:
	ANBS N 3 A_Pain
	ANBS OPQRS 4
	Goto See
	Death:
	ANBS T 6 A_PlaySound("anubisdeath")
	ANBS UVWX 4
	ANBS YYYYYYYYYYYYYYY 0 A_CustomMissile("Minismokeeffect3", 0, 0, random(0,360), 2, 0)
	ANBS Y 4 A_Fall
	ANBS Y 200
	Goto Fade
	Fade:
	ANBS Y 2 A_FadeOut(0.2)
	Loop
	Melee:
	ANBS I 0 A_PlaySound("anubissee")
	ANBS HI 2 A_FaceTarget
	ANBS I 0 A_SkelFist
	ANBS JKLM 4
	Goto See
	}
}

actor Greenportal
{
+LOOKALLAROUND
radius 2
Height 2
- SOLID
+NODAMAGE
+NOBLOOD
+NOGRAVITY
renderstyle Add
alpha 0.8
Scale 0.8
States
{
Spawn:
PR1S A 0
PR1S A 0 A_PlaySound("greenportalopen")
PR1S ABCDEFGHIJKLMNOPQRSTU 2 bright A_SpawnItemEx("smallgreenfogspawner",0,0,0,0,0,0,0,160,0)
Goto Death
Death:
PR1E ABCDEFGHIJKLM 2 bright
Stop
}
}

actor smallgreenfogspawner
{
- SOLID
+NOGRAVITY
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CustomMissile("smallgreenfog", 0, 0, random(0,360), 2, random(-90,90))
Stop
}
}

actor smallgreenfog
{
scale 0.2
alpha 0.5
- SOLID
PROJECTILE
//Damage 0
renderstyle Add
+BLOODLESSIMPACT
+NOBLOOD
Speed 2
States
{
Spawn:
PR1E ABCDEFGHIJKLM 2 bright
Stop
}
}

actor mediumgreenfog : smallgreenfog
{
scale 0.5
speed 4
}
actor AnubisTracer
{
  radius 20
  height 20
  speed 24
  damage 12
  renderstyle Add
  alpha 0.8
  PROJECTILE
  +SEEKERMISSILE
  +RANDOMIZE
  +DOOMBOUNCE
  +SHOOTABLE
  - NOBLOCKMAP
  health 5
  bloodtype "mediumgreenfog"
  mass 999999
  translation "192:207=112:127"
  scale 0.4
  bouncefactor 0.3
  reactiontime 45
  deathsound "anubisfireballhit"
  states
  {
  Spawn:
   	GBUB A 0 
 	GBUB A 0 A_Playsound("anubisfireball")
    GBUB A 2 bright A_SeekerMissile (2,5)
	GBUB A 0 A_SpawnItemEx("mediumgreenfog",0,0,0,0,0,0,0,160,0)
    GBUB B 2 bright A_SeekerMissile (2,5)
    GBUB C 2 bright A_SeekerMissile (2,5)
	GBUB A 0 A_SpawnItemEx("mediumgreenfog",0,0,0,0,0,0,0,160,0)
    GBUB D 2 bright A_SeekerMissile (2,5)
	GBUB A 0 A_CountDown
    Goto Spawn+2
  Death:
	PR1E ABCDEFGHIJKLM 1 
    stop
  }
}

ACTOR Eyesore 
{
	Tag "Eyesore"
	Health 2000
	Radius 13
	Height 35
	Speed 13
	scale 0.4
	MASS 200
	MELEEDAMAGE 3
	MONSTER 
	+FLOORCLIP
	+DROPOFF
	+DONTBLAST
	+LOOKALLAROUND
	+DONTMORPH
	+NOICEDEATH
	MeleeSound "Freak2"
	DeathSound "Freak5"
	ActiveSound "FreakR"
	PainSound "Freak4"
	Obituary "%o was munched by an eyesore."
	// Sorry Complex doom has no yellow blood yet.
	BloodType "BlackBlood"
	BloodColor "30 30 30"
	Species "Undead"
	DropItem "LifeEssence" 80
	DropItem "ArmorBonusMax" 80
	States
	{
	Spawn:
		THIN A 1 A_Look
		Loop
	See:
		THIN AAABBB 1 A_Chase
		Loop
	Melee:
		THIN A 3 A_MeleeAttack
		THIN A 1
		Goto See
	Death:
		THIN A 2 A_Scream
		THIN C 3 A_fall
		THIN D 3
		THIN E 3
		THIN F 3
		THIN H 3 A_PlaySound("vile/firecrkl")
		FIRE A 0 BRIGHT A_CustomMissile("ArchvileFirex",0,0,0,2)
		FIRE A 0 BRIGHT A_CustomMissile("ArchvileFirex",0,0,30,2)
		FIRE A 0 BRIGHT A_CustomMissile("ArchvileFirex",0,0,60,2)
		FIRE A 0 BRIGHT A_CustomMissile("ArchvileFirex",0,0,90,2)
		FIRE A 0 BRIGHT A_CustomMissile("ArchvileFirex",0,0,120,2)
		FIRE A 0 BRIGHT A_CustomMissile("ArchvileFirex",0,0,150,2)
		FIRE A 0 BRIGHT A_CustomMissile("ArchvileFirex",0,0,180,2)
		FIRE A 0 BRIGHT A_CustomMissile("ArchvileFirex",0,0,-30,2)
		FIRE A 0 BRIGHT A_CustomMissile("ArchvileFirex",0,0,-60,2)
		FIRE A 0 BRIGHT A_CustomMissile("ArchvileFirex",0,0,-90,2)
		FIRE A 0 BRIGHT A_CustomMissile("ArchvileFirex",0,0,-120,2)
		FIRE A 0 BRIGHT A_CustomMissile("ArchvileFirex",0,0,-150,2)
		Stop
	}
}

// --->
// Invasion Spawn Spots
// --->
actor SkeletonArcherSpot : CustomMonsterInvasionSpot 12350
{DropItem SkeletonArcher}

actor BruteSpot : CustomMonsterInvasionSpot 12352
{DropItem Brute}

actor GolemRiderSpot : CustomMonsterInvasionSpot 12353
{DropItem GolemRider}

actor AnubisSpot : CustomMonsterInvasionSpot 15072
{DropItem Anubis}

actor EyesoreSpot : CustomMonsterInvasionSpot 12348
{DropItem Eyesore}
