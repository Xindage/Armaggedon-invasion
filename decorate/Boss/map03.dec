//------------\\
// Map03 Boss \\
//------------\\
actor GiantSandWorm
{
Height 300
Radius 50
Speed 33
meleerange 100
Scale 0.8
Health 1500
BloodColor "Green"
Obituary "%o was roasted by a Giant Sand Worm."
deathsound "sandwormdeath"
//activesound "sandwormact"
painsound "sandwormact"
mass 9999999999
maxstepheight 1
MONSTER
+BOSS
+NORADIUSDMG
+FLOORHUGGER
+CANTLEAVEFLOORPIC
//rollingrock
painchance 1
States
{
Spawn:
//SWRM NOPR 4
//SWRM A 0 A_ChangeFlag("SHOOTABLE", 0)
//SWRM A 0 A_ChangeFlag("INVULNERABLE", 1)
TNT1 A 0 A_ChangeFlag("SHOOTABLE", 0)
TNT1 A 0 A_ChangeFlag("INVULNERABLE", 1)
TNT1 A 2 A_Look
Goto Spawn+4
See:
SWRM A 0 A_ChangeFlag("SHOOTABLE", 0)
SWRM A 0 A_ChangeFlag("INVULNERABLE", 1)
TNT1 A 2 A_Chase
TNT1 A 0 Radius_Quake(2, 3, 0, 300, 0)
TNT1 A 0 A_SpawnItemEx("SandWormDirtfx", random(-10,10), random(-10,10), 0, 0, 0, 0, 0, 128, 0)
TNT1 A 0 A_JumpIfCloser(700,"Detect")
Loop
Detect:
TNT1 A 2 A_Chase
TNT1 A 0 A_FaceTarget
TNT1 A 0 A_SpawnItemEx("SandWormDirtfx", random(-10,10), random(-10,10), 0, 0, 0, 0, 0, 128, 0)
TNT1 A 0 Radius_Quake(2, 3, 0, 300, 0)
TNT1 A 0 A_GiveInventory("Clip",1)
TNT1 A 0 A_JumpIfInventory("Clip", 80, "Flamethower")
TNT1 A 0 A_JumpIfCloser(100,"Melee")
Loop
Melee:
SWRM A 0 A_ChangeFlag("SHOOTABLE", 1)
SWRM A 0 A_ChangeFlag("INVULNERABLE", 0)
SWRM A 0 A_SpawnItemEx("SandWormBurstfxspawner", 0, 0, 0, 0, 0, 0, 0, 128, 0)
SWRM XWVU 3
SWRM NOPQ 2
SWRM Q 0 A_JumpIfCloser(400,"Meleesuccess")
SWRM R 3
Goto Meleefinish
Meleesuccess:
SWRM R 3 A_GiveToTarget("SandWormChomp",1)
Goto Meleefinish
Meleefinish:
SWRM SMLK 7
SWRM A 0 A_SpawnItemEx("SandWormBurstfxspawner", 0, 0, 0, 0, 0, 0, 0, 128, 0)
SWRM A 0 A_ChangeFlag("SHOOTABLE", 0)
SWRM A 0 A_ChangeFlag("INVULNERABLE", 1)
TNT1 A 0 A_TakeInventory("Clip",50)
Goto See
Flamethower:
SWRM A 0 A_ChangeFlag("SHOOTABLE", 1)
SWRM A 0 A_ChangeFlag("INVULNERABLE", 0)
SWRM A 0 A_SpawnItemEx("SandWormBurstfxspawner", 0, 0, 0, 0, 0, 0, 0, 128, 0)
SWRM XWVU 3
SWRM ABC 5 A_FaceTarget
SWRM EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 1 A_CustomMissile("Wormflame", 90, 0, random(-25,25), 2, random(-15,0))
SWRM FG 9
Goto Meleefinish
Pain:
SWRM H 0 A_ChangeFlag("INVULNERABLE", 1)
SWRM H 0 A_Pain
SWRM HIJKL 2
SWRM M 7
SWRM SMLK 2
SWRM A 0 A_SpawnItemEx("SandWormBurstfxspawner", 0, 0, 0, 0, 0, 0, 0, 128, 0)
TNT1 A 0 A_TakeInventory("Clip",50)
Goto Hidesee
Hidesee:
SWRM A 0 A_ChangeFlag("INVULNERABLE", 1)
SWRM A 0 A_ChangeFlag("FRIGHTENED", 1)
TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_Chase
SWRM A 0 A_ChangeFlag("FRIGHTENED", 0)
Goto See
Death:
SWRM H 0 A_Scream
SWRM HIJKLHIJKLHIJKLHIJKLHIJKLHIJKLHIJKLHIJKL 1
SWRM TUVWX 4
SWRM Y 0 A_Fall
SWRM Y 0 A_PlaySound("rockexplode")
SWRM Y 200 A_SpawnItemEx("SandWormDeath", 0, 0, 0, 0, 0, 0, 0, 128, 0)
Goto Fade
Fade:
SWRM Y 1 A_FadeOut(0.05)
Loop
}
}

actor SandWormDeath
{
- SOLID
+NOBLOCKMAP
height 10
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_PlaySound("")
TNT1 AAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("SandWormSmokeeffect",0,0,random(0,360),2,0)
Stop
}
}

actor Wormflame
{
PROJECTILE
radius 5
height 5
speed 20
+DOOMBOUNCE
damagetype "Fire"
scale 1.3
renderstyle translucent
alpha 0.6
seesound "sandwormfire"
States
{
Spawn:
CFFX ABCDEFGHIJKL 2 A_Explode(20, 130, 0)
Stop
}
}

actor SandWormBurstfxspawner
{
- SOLID
+NOBLOCKMAP
+ALLOWCLIENTSPAWN
States
{
Spawn:
TNT1 A 0
//TNT1 A 0 A_Explode(30, 900, 1)
TNT1 A 0 A_SpawnItemEx("SandWormBurstfx", 60, 0, 0, 0, 0, 0, 0, 128, 0)
TNT1 A 0 A_SpawnItemEx("SandWormBurstfx", -60, 0, 0, 0, 0, 0, 0, 128, 0)
TNT1 A 0 A_SpawnItemEx("SandWormBurstfx", 0, 60, 0, 0, 0, 0, 0, 128, 0)
TNT1 A 0 A_SpawnItemEx("SandWormBurstfx", 0, -60, 0, 0, 0, 0, 0, 128, 0)
TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("SandWormSmokeeffect",0,0,random(0,360),2,random(70,90))
TNT1 A 1 A_PlaySound("SandWormExplode")
Stop
}
}

actor SandWormDirtfx
{
- SOLID
+NOBLOCKMAP
+ALLOWCLIENTSPAWN
States
{
Spawn:
TNT1 AAA 0 A_CustomMissile("SandWormSmokeeffect",0,0,random(0,360),2,random(50,90))
Stop
}
}

actor SandWormBurstfx
{
- SOLID
+ALLOWCLIENTSPAWN
+NOBLOCKMAP
damage 0
renderstyle translucent
alpha 0.9
scale 1.5
States
{
Spawn:
MNSM A 0
//MNSM A 0 A_Explode(20, 900, 0)
MNSM A 5
MNSM ABCDEFGHIJKLMNOPQ 5 A_Fadeout(0.05)
Stop
}
}

actor SandWormSmokeeffect : Smokeeffect
{
speed 18
- NOGRAVITY
Scale 0.8
+ALLOWCLIENTSPAWN
Alpha 0.3
translation "80:111=208:223", "0:15=208:223"
States
{
Spawn:
SMOK ABCDEFGHIJKLMNOPQ 2
Stop
}
}

actor SandWormChomp : CustomInventory
{
Obituary "%o was swallowed whole by a Giant Sand Worm."
States
{
Spawn:
TNT1 A 1
Loop
Pickup:
TNT1 A 0 A_PlaySound("sandwormbite")
TNT1 A 0 DamageThing(0)
Stop
}
}

actor GiantSandWormpot : CustomMonsterInvasionSpot 12355
{DropItem GiantSandWorm}