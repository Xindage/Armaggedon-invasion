// --->
// StoneGolem - This bad boy uses 450 lines of decorate alone.
// --->
// Changed the rocket ammo usage by golem tokens, I hope that fix it's bugged
// behavior.
actor StoneGolem
{
	Health 15000
	Radius 100
	Height 760
	Speed 5
	PainChance 1
	Scale 4.0
	MONSTER 
	+NOBLOOD
	+FLOORCLIP
	+THRUGHOST // Why?
	+DONTHARMSPECIES
//	+DONTTHRUST
	renderstyle Translucent
	alpha 1.0
	Obituary "%o was crushed by a giant rock golem."
	ActiveSound "golemmove"
	SeeSound "golemsee"
	DeathSound "golemdie"
	mass 150000
	Species "Rock"
	States
	{
	Spawn:
		EREL B 0
		EREL B 20 A_Look
		Loop
	See:
		EREL BBBBBB 5 
		EREL B 0 A_JumpIfInventory("StoneGolemToken", 3, "Attackpick")
		EREL B 0 A_GiveInventory("StoneGolemToken",random(1,3))
		EREL CCCCCCDDDDDDEEEEEE 4 A_Chase
		EREL F 0 A_SpawnItemEx("GiantRockGolemStomp", 0, 0, 0, 0, 0, 0, 0, 128, 0)
		EREL F 0 Radius_Quake(5, 30, 12, 300, 0)
		EREL FFFFFF 5
		EREL F 0 A_JumpIfInventory("StoneGolemToken", 3, "Attackpick")
		EREL B 0 A_GiveInventory("StoneGolemToken",random(1,3))
		EREL GGGGGGHHHHHHIIIIII 4 A_Chase
		EREL B 0 A_SpawnItemEx("GiantRockGolemStomp", 0, 0, 0, 0, 0, 0, 0, 128, 0)
		EREL B 0 Radius_Quake(5, 30, 12, 300, 0)
		Loop
	Attackpick:
		EREL A 0 A_TakeInventory("StoneGolemToken",1000) 
		EREL A 0 A_CheckSight("See")
		EREL A 0 A_Jump(256, "Rocksummon1", "Rocksummon2", "Rollingattack", "Rollingattack")
		EREL A 1
		Goto See
	Rocksummon1:
		EREL A 0 A_PlaySound("golemattack")
		EREL J 0 A_TakeInventory("StoneGolemToken",1000) 
		EREL JKL 10 A_FaceTarget
		EREL L 0 Radius_Quake(2, 30, 0, 800, 0)
		EREL L 0 A_CustomMissile("Summoningrock1", 0, -250,0 ,0)
		EREL L 0 A_CustomMissile("Summoningrock2", 0, 250,0 ,0)
		EREL L 0 A_PlaySound("golemsee")
	Goto Rocktimer
	Rocksummon2:
		EREL A 0 A_PlaySound("golemattack")
		EREL J 0 A_TakeInventory("StoneGolemToken",1000) 
		EREL JKL 10 A_FaceTarget
		EREL L 0 Radius_Quake(2, 30, 0, 800, 0)
		EREL L 0 A_CustomMissile("Summoningrock2", 0, -250,0 ,0)
		EREL L 0 A_CustomMissile("Summoningrock1", 0, 250,0 ,0)
		EREL L 0 A_PlaySound("golemsee")
	Goto Rocktimer
	Rocktimer:
		EREL L 1 A_GiveInventory("StoneGolemToken",1)
		EREL L 0 A_FaceTarget
		EREL L 0 A_JumpIfInventory("StoneGolemToken", 200, "Rockfinish")
		Loop
	Rockfinish:
		EREL LKJ 10 A_TakeInventory("StoneGolemToken",1000) 
		Goto See
	Rollingattack:
		EREL A 0 A_FaceTarget
		EREL A 50 A_SpawnItem("Summoningrollingrock", 250)
		EREL A 0 A_PlaySound("golemattack")
		EREL JKL 8
		EREL M 13 A_SpawnItem("Launchcheck", 250,356)
		EREL N 10 A_TakeInventory("StoneGolemToken",1000) 
		EREL OP 10
		Goto See
	Death:
		EREL Z 0 A_Fall
		EREL Q 10 A_Scream
		EREL QQQQQ 6 
		EREL RRRRR 6 
		EREL WWWWW 4 
		ERED A 0 Radius_Quake(6, 100, 0, 300, 0)
		ERED A 0 A_PlaySound("golemland")
		EREL XXXX 4 
		EREL YYYYYYY 2 
		ERED A 0 Radius_Quake(6, 100, 0, 300, 0)
		ERED A 0 A_PlaySound("golemland")
		EREL ZZ 5 
		ERED AA 5 
		ERED AA 5 
		ERED A 300
		Goto Fading
	Fading:
		ERED A 8 A_FadeOut(0.1)
		Loop
	}
}

// --->
// Extra Data & Projectiles for Golem
// --->
Actor StoneGolemToken : Inventory { Inventory.Amount 1 Inventory.MaxAmount 600 }

actor Golemdeathspawnfx // Seems Unused
{
	-SOLID
	+NOBLOCKMAP
	+LOOKALLAROUND
	+ALLOWCLIENTSPAWN
	States
	{
	Spawn:
		TNT1 AAAAA 1 A_Look
	See:
		TNT1 A 0 A_PlaySound("rockexplode")
		TNT1 A 1 A_CustomMissile("rollingrocktrail2", 0, random(-200,200), 0, 2,0)
		Stop
	}
}

actor Golemdeathspawnfx2 // Seems Unused
{
	-SOLID
	+ALLOWCLIENTSPAWN
	+NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 AAAAA 1 A_Look
	See:
		TNT1 A 1 A_CustomMissile("GiantRockGolemStomp", 0,random(-200,200), 0, 2,0)
		Stop
	}
}
actor Launchcheck // What is the use of it?
{
	damagetype "Launch"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1 A_Explode(10, 200, 0)
		Stop
	}
}

actor Summoningrock1
{
	Obituary "%o was crushed by a giant rock golem."
	+NOGRAVITY
	Speed 0
	+LOOKALLAROUND
	Scale 1.5
	+SHOOTABLE
	Health 70
	+NOBLOOD
	Height 110
	Radius 50
	mass 999999
	States
	{
	Spawn:
		RCKF A 0
		RCKF AAAA 1 A_Look
		Stop
	See:
		ROLR A 0 A_PlaySound("rockspawn")
		RCKF A 0 A_SpawnItemEx("Spawnrockfx", 0, 0, 0, 0, 0, 0, 0, 128, 0)
		RCKF A 80 ThrustThingZ(0, 28, 0, 1)
		RCKF A 10 A_Stop
		RCKF A 0 A_FaceTarget
		RCKF A 0 A_CustomMissile("bigrockprojectile",0,0,0)
		Stop
	Death:
		TNT1 A 1 A_Explode(50,700,1)
		TNT1 A 0 A_PlaySound("rockexplode")
		TNT1 A 0 A_SpawnItemEx("Spawnrockfx2", 0, 0, 0, 0, 0, 0, 0, 128, 0)
		Stop
	}
}

Actor Spawnrockfx
{
	renderstyle none
	-SOLID
	+ALLOWCLIENTSPAWN
	States
	{
	Spawn:
		RCKF A 0
		RCKF AAAAA 0 A_CustomMissile("Smokeeffect", random(0,10), 0, random(0,360), 2, random(10,90))
		RCKF AAAAA 0 A_CustomMissile("Minismokeeffect", random(0,10), 0, random(0,360), 2, random(10,90))
		RCKF A 1
		Stop
	}
}

Actor Spawnrockfx2 : Spawnrockfx
{
	+ALLOWCLIENTSPAWN
	States
	{
	Spawn:
		TNT1 AAAAAAAAAA 0 A_CustomMissile("minirockeffect", 0, 0, random(0,360), 2, random(10,90))
		TNT1 AAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("Minismokeeffect2", 0, 0, random(0,360), 2, random(0,90))
		TNT1 A 1
		Stop
	}
}

actor Summoningrock2 : Summoningrock1
{
	States
	{
	See:
		ROLR A 0 A_PlaySound("rockspawn")
		RCKF A 80 ThrustThingZ(0, 28, 0, 1)
		RCKF A 30 A_Stop
		RCKF A 0 A_FaceTarget
		RCKF A 0 A_CustomMissile("bigrockprojectile",0,0,0)
		TNT1 A 0 A_SpawnItemEx("Spawnrockfx2", 0, 0, 0, 0, 0, 0, 0, 128, 0)
		Stop
	}
}

actor Summoningrollingrock
{
	Obituary "%o was crushed by a giant rock golem's rolling stone."
	mass 999999
	+NOGRAVITY
	Speed 0
	+LOOKALLAROUND
	Scale 1.5
	-SHOOTABLE
	Health 120
	+NOBLOOD
	Height 50
	Radius 60
	painchance 0
	States
	{
	Spawn:
		ROLR A 0
		ROLR AAAA 1 A_Look
		Stop
	See:
		ROLR A 0 A_PlaySound("rockspawn")
		ROLR A 0 A_SpawnItemEx("Spawnrockfx", 0, 0, 0, 0, 0, 0, 0, 128, 0)
		ROLR A 70 ThrustThingZ(0, 28, 0, 1)
		ROLR A 0 A_FaceTarget
		ROLR A 10 A_Stop
		Goto Pain.Launch
	Death:
		TNT1 A 1 A_Explode(80,500,1)
		TNT1 A 0 A_Playsound("rockexplode")
		ROLR A 0 A_SpawnItemEx("Spawnrockfx2", 0, 0, 0, 0, 0, 0, 0, 128, 0)
		Stop
	Pain.Launch:
		ROLR A 0 A_PlaySound("rockexplode")
		//ROLR A 5 ThrustThingZ(0, 200, 1, 1)
		ROLR A 1 A_CustomMissile("rollingrockprojectile",0,0,0,2,0)
		Stop
	}
}

actor GiantRockGolemStomp
{
	-SOLID
	+NOCLIP
	+ALLOWCLIENTSPAWN
	+NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A 0 A_PlaySound("golemstomp")
		Stop
	}
}

actor Smokeeffect
{
	+ALLOWCLIENTSPAWN
	renderstyle translucent
	speed 18
	+DONTSPLASH
	alpha 0.8
	radius 4
	height 4
	+DOOMBOUNCE
	damage 0
	+RIPPER
	+NOBLOOD
	States
	{
	Spawn:
		SMOK ABCDEFGHIJKLMNOPQ 2
		Stop
	}
}

//actor Minismokeeffect : Smokeeffect
//{
//speed 24
//Scale 0.5
//alpha 0.5
//}

//actor Minismokeeffect2 : Minismokeeffect
//{
//Scale 0.9
//speed 20
//}

actor bigrockprojectile
{
Height 100
Radius 50
Damage 20
damagetype "Rock"
PROJECTILE
+SHOOTABLE
Health 40
+NOBLOOD
Speed 45
Scale 1.5
States
{
Spawn:
RCKF ABCDEFGHIJKLMNOPQRSTUVWXYZ 3
RCK2 ABCDEFGHIJK 3
loop
Death:
TNT1 A 1 A_Explode(70,300,1)
TNT1 A 0 A_Playsound("rockexplode")
ROLR A 0 A_SpawnItemEx("Spawnrockfx2", 0, 0, 0, 0, 0, 0, 0, 128, 0)
Stop
}
}

actor minirockeffect
{
Scale 0.2
Speed 12
Height 4
+ALLOWCLIENTSPAWN
Radius 4
PROJECTILE
- NOGRAVITY
Damage 0
States
{
Spawn:
RCKF ABCDEFGHIJKLMNOPQRSTUVWXYZ 1
RCK2 ABCDEFGHIJK 1
Goto Death
Death:
TNT1 A 1
Stop
}
}

actor rollingrockprojectile
{
Radius 50
Damage 50
damagetype "Rock"
Obituary "%o was crushed by a giant rock golem's rolling stone."
Height 50
Speed 70
Scale 1.5
PROJECTILE
- NOGRAVITY
+RIPPER
+NOCLIP
+FLOORHUGGER
States
{
Spawn:
ROLR A 0 
ROLR ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_SpawnItem("rollingrocktrail",-9)
ROL2 ABCDEF 1 A_SpawnItem("rollingrocktrail",-10)
ROL2 A 0 A_GiveInventory("Clip",1)
ROL2 A 0 A_JumpIfInventory("Clip",20,"Death")
loop
Death:
TNT1 A 1 A_Explode(30,200,1)
TNT1 A 0 A_Playsound("rockexplode")
ROLR A 0 
Stop
}
}

actor rollingrocktrail
{
-SOLID
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_PlaySound("rollingrock")
ROLR A 0 A_SpawnItemEx("rollingrockfx", 0, 0, 0, 0, 0, 0, 0, 128, 0)
TNT1 A 1 A_Explode(18, 300,1)
Stop
}
}

actor rollingrocktrail2 : rollingrocktrail
{
+ALLOWCLIENTSPAWN
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_PlaySound("rollingrock")
TNT1 A 1 A_SpawnItemEx("rollingrockfx", 0, 0, 0, 0, 0, 0, 0, 128, 0)
Stop
}
}

actor rollingrockfx : Spawnrockfx
{
+ALLOWCLIENTSPAWN
States
{
Spawn:
TNT1 A 1
Stop
}
}

actor StoneGolemSpot : CustomMonsterInvasionSpot 12354
{DropItem StoneGolem}
