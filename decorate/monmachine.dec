//Decorate data base of monsters based on strife.
//Alpha status: Complete.
//Todo: Finish Inquisitor dependency

// --->
// Reaver
// --->
ACTOR Reaver~
{  
	Health 150
	Radius 20
	Height 60
	Speed 12
	Mass 500
	Painchance 128
	Species "Machines"
	DropItem "LifeEssence" 50
	DropItem "ArmorBonusMax" 50
	DropItem "NewCell" 50 20
	MONSTER 
	+NOBLOOD
	+NOICEDEATH
	+DONTHARMSPECIES
	SeeSound "reaver/sight"
	PainSound "reaver/pain"
	DeathSound "reaver/death"
	ActiveSound "reaver/active"
	HitObituary "%o was sliced open by a Reaver."
	Obituary "%o was shot down by a Reaver."
	States
	{
	Spawn:
		ROB1 A 10 A_Look
		Loop
	See:
		ROB1 BBCCDDEE 3 A_Chase
		Loop
	Melee:
		ROB1 H 6 A_FaceTarget
		ROB1 I 8 A_CustomMeleeAttack(random[ReaverMelee](1,8)*3, "reaver/blade")
		ROB1 H 6
		Goto See
	Missile:
		ROB1 F 8 A_FaceTarget
		ROB1 G 0 A_Playsound("reaver/attack")
		ROB1 G 0 A_CustomMissile("CrossbowFX3", 20, 0, -15)
		ROB1 G 0 A_CustomMissile("CrossbowFX3", 20, 0, 15)
		ROB1 G 11 A_CustomMissile("CrossbowFX1", 20, 0, 0)
		Goto See
	Pain:
		ROB1 A 2
		ROB1 A 2 A_Pain
		Goto See
	Death:
		ROB1 J 6
		ROB1 K 6 A_Scream
		ROB1 L 5
		ROB1 M 5 A_NoBlocking
		ROB1 NOP 5
		ROB1 Q 6 A_Explode(32,32,1)
		ROB1 R -1
		Stop
	}
}

// --->
// Crusader
// --->
ACTOR Crusader~
{
	Health 650
	Radius 40
	Height 56
	Speed 8
	Mass 400
	Painchance 15
	Species "Machines"
	DropItem "LifeEssence" 75
	DropItem "ArmorBonusMax" 75
	DropItem "MissileBox" 100 10
	MONSTER 
	+NOBLOOD
	+DONTMORPH
	+MISSILEMORE
	+NOICEDEATH
	+DONTHARMSPECIES
	SeeSound "crusader/sight"
	PainSound "crusader/pain"
	DeathSound "crusader/death"
	ActiveSound "crusader/active"
	Obituary "%o was swept away by a Crusader."
	States
	{
	Spawn:
		ROB2 Q 10 A_Look
		Loop
	See:
		ROB2 AABBCCDD 3 A_Chase
		Loop
	Missile:
		ROB2 E 9 A_JumpIfCloser(260,"Flamethrowers")
		ROB2 E 10 A_FaceTarget
		ROB2 F 10 A_CustomMissile("CyberLordRocket", 25, -28, 0)
		goto see
	Flamethrowers:
		ROB2 EE 8 A_FaceTarget
		ROB2 F 0 A_FaceTarget
		ROB2 F 1 Bright A_CustomMissile("HectebusFlamethrower",30,-30,FRandom(6,-6),CMF_AIMOFFSET)
		ROB2 F 0 A_FaceTarget
		ROB2 F 1 Bright A_CustomMissile("HectebusFlamethrower",30,-30,FRandom(6,-6),CMF_AIMOFFSET)
		ROB2 F 0 A_FaceTarget
		ROB2 F 1 Bright A_CustomMissile("HectebusFlamethrower",30,-30,FRandom(6,-6),CMF_AIMOFFSET)
		ROB2 F 0 A_FaceTarget
		ROB2 F 1 Bright A_CustomMissile("HectebusFlamethrower",30,-30,FRandom(6,-6),CMF_AIMOFFSET)
		ROB2 F 0 A_FaceTarget
		ROB2 F 1 Bright A_CustomMissile("HectebusFlamethrower",30,-30,FRandom(6,-6),CMF_AIMOFFSET)
		ROB2 F 0 A_FaceTarget
		ROB2 F 1 Bright A_CustomMissile("HectebusFlamethrower",30,-30,FRandom(6,-6),CMF_AIMOFFSET)
		ROB2 F 0 A_FaceTarget
		ROB2 F 1 Bright A_CustomMissile("HectebusFlamethrower",30,-30,FRandom(6,-6),CMF_AIMOFFSET)
		ROB2 F 0 A_FaceTarget
		ROB2 F 1 Bright A_CustomMissile("HectebusFlamethrower",30,-30,FRandom(6,-6),CMF_AIMOFFSET)
		ROB2 F 0 A_FaceTarget
		ROB2 F 1 Bright A_CustomMissile("HectebusFlamethrower",30,-30,FRandom(6,-6),CMF_AIMOFFSET)
		ROB2 F 0 A_FaceTarget
		ROB2 F 1 Bright A_CustomMissile("HectebusFlamethrower",30,-30,FRandom(6,-6),CMF_AIMOFFSET)
		ROB2 F 0 A_FaceTarget
		ROB2 F 1 Bright A_CustomMissile("HectebusFlamethrower",30,-30,FRandom(6,-6),CMF_AIMOFFSET)
		ROB2 F 0 A_FaceTarget
		ROB2 F 1 Bright A_CustomMissile("HectebusFlamethrower",30,-30,FRandom(6,-6),CMF_AIMOFFSET)
		ROB2 F 0 A_FaceTarget
		ROB2 F 1 Bright A_CustomMissile("HectebusFlamethrower",30,-30,FRandom(6,-6),CMF_AIMOFFSET)
		ROB2 F 0 A_FaceTarget
		ROB2 F 1 Bright A_CustomMissile("HectebusFlamethrower",30,-30,FRandom(6,-6),CMF_AIMOFFSET)
		ROB2 F 0 A_FaceTarget
		ROB2 F 1 Bright A_CustomMissile("HectebusFlamethrower",30,-30,FRandom(6,-6),CMF_AIMOFFSET)
		ROB2 F 0 A_FaceTarget
		ROB2 F 1 Bright A_CustomMissile("HectebusFlamethrower",30,-30,FRandom(6,-6),CMF_AIMOFFSET)
		ROB2 F 0 A_FaceTarget
		ROB2 F 1 Bright A_CustomMissile("HectebusFlamethrower",30,-30,FRandom(6,-6),CMF_AIMOFFSET)
		ROB2 F 0 A_FaceTarget
		ROB2 F 1 Bright A_CustomMissile("HectebusFlamethrower",30,-30,FRandom(6,-6),CMF_AIMOFFSET)
		ROB2 F 0 A_FaceTarget
		ROB2 F 1 Bright A_CustomMissile("HectebusFlamethrower",30,-30,FRandom(6,-6),CMF_AIMOFFSET)
		ROB2 F 0 A_FaceTarget
		ROB2 F 1 Bright A_CustomMissile("HectebusFlamethrower",30,-30,FRandom(6,-6),CMF_AIMOFFSET)
		ROB2 E 8
		goto see
	Pain:
		ROB2 D 1 A_Pain
		Goto See
	Death:
		ROB2 G 3 A_Scream
		ROB2 H 5 
		ROB2 I 4 Bright 
		ROB2 J 4 Bright A_Explode(64,64,1)
		ROB2 K 4 Bright A_Scream
		ROB2 L 4 
		ROB2 MN 4 A_NoBlocking
		ROB2 O 4 
		ROB2 P -1 
		Stop
	}
}

// --->
// Inquisitor
// Should be standalone
// --->
ACTOR Inquisitor~ : Inquisitor 
{
	Game Strife
	Health 1000
	Speed 12
	Radius 40
	Height 110
	Mass 0x7fffffff
	Monster
	+DROPOFF
	+NOBLOOD
	+DONTMORPH
	+MISSILEMORE
	+NOICEDEATH
	+DONTHARMSPECIES
	Species "Machines"
	DropItem "LifeCore" 64 40
	DropItem "BossArmorBonusMax" 64 1
	DropItem "MissileBox" 128 20
	MaxDropOffHeight 32
	MinMissileChance 150
	SeeSound "inquisitor/sight"
	DeathSound "inquisitor/death"
	ActiveSound "inquisitor/active"
	Obituary "$OB_INQUISITOR"
	states
	{
	Spawn:
		ROB3 AB 10 A_Look
		Loop
	See:
		ROB3 BBCC 3 A_Chase
		ROB3 B 0 A_PlaySound("inquisitor/walk")
		ROB3 DDEE 3 A_Chase
		ROB3 E 0 A_PlaySound("inquisitor/walk")
		Loop
	Missile:
		ROB3 A 2 A_JumpIfCloser(500, "Grenades")
		ROB3 F 6 A_FaceTarget
		ROB3 G 4 Bright A_CustomMissile("CyberLordRocket", 64, -40, 0, 0)
		ROB3 F 4 A_FaceTarget
		ROB3 G 4 Bright A_CustomMissile("CyberLordRocket", 64, -40, 0, 0)
		ROB3 F 4
		Goto See
	Grenades:
		ROB3 K 0 A_Jump (64, "CluterGrenades")
		ROB3 K 9 A_FaceTarget
		ROB3 K 3 A_PlaySound("Weapons/GrenadeFire")
		ROB3 J 0 Bright A_CustomMissile("RifleGrenade", 100, 20, -15, -15)
		ROB3 J 3 Bright A_CustomMissile("RifleGrenade", 100, -20, 15, -15)
		ROB3 K 3 A_FaceTarget
		ROB3 J 0 Bright A_CustomMissile("RifleGrenade", 100, 20, -15, -15)
		ROB3 J 3 Bright A_CustomMissile("RifleGrenade", 100, -20, 15, -15)
		ROB3 K 3 A_FaceTarget
		ROB3 J 0 Bright A_CustomMissile("RifleGrenade", 100, 20, -15, -15)
		ROB3 J 3 Bright A_CustomMissile("RifleGrenade", 100, -20, 15, -15)
		ROB3 K 3 A_FaceTarget
		ROB3 J 0 Bright A_CustomMissile("PClusterGrenade", 100, 20, -15, -15)
		ROB3 J 3 Bright A_CustomMissile("PClusterGrenade", 100, -20, 15, -15)
		ROB3 K 3 
		ROB3 K 9 A_PlaySound("Weapons/GrenadeCycle")
		Goto See
	CluterGrenades:
		ROB3 K 9 A_FaceTarget
		ROB3 K 3 A_PlaySound("Weapons/GrenadeFire")
		ROB3 J 0 Bright A_CustomMissile("PClusterGrenade", 100, 20, -15, -15)
		ROB3 J 3 Bright A_CustomMissile("PClusterGrenade", 100, -20, 15, -15)
		ROB3 K 3 A_FaceTarget
		ROB3 J 0 Bright A_CustomMissile("PClusterGrenade", 100, 20, -15, -15)
		ROB3 J 3 Bright A_CustomMissile("PClusterGrenade", 100, -20, 15, -15)
		ROB3 K 3 A_FaceTarget
		ROB3 J 0 Bright A_CustomMissile("PClusterGrenade", 100, 20, -15, -15)
		ROB3 J 3 Bright A_CustomMissile("PClusterGrenade", 100, -20, 15, -15)
		ROB3 K 3 A_FaceTarget
		ROB3 J 0 Bright A_CustomMissile("PClusterGrenade", 100, 20, -15, -15)
		ROB3 J 3 Bright A_CustomMissile("PClusterGrenade", 100, -20, 15, -15)
		ROB3 K 3 
		ROB3 K 9 A_PlaySound("Weapons/GrenadeCycle")
		Goto See
	}
}

// --->
// LightningSentry
// --->
actor LightningSentry
{
	Height 150
	Radius 30
	Health 1350
	MONSTER
	-SOLID
	+NOBLOOD
	+LOOKALLAROUND
	+QUICKTORETALIATE
	+DONTHARMSPECIES
	+FULLVOLDEATH
	+MISSILEMORE
	+DONTHURTSPECIES
	+DONTTHRUST
	Speed 0
	Scale 0.7
	Mass 100
	Species "Machines"
	Damagefactor "Lightning", 0.0
	Renderstyle translucent
	Deathsound "lasersentrydeath"
	Obituary "%o was electrocuted by a lightning sentry."
	Alpha 1.0
	States
	{
	Spawn:
		LSEN BCDEFGHIJKLBCDEFGHIJK 1 A_Look
		Loop
	See:
		LSEN BCDEFGHIJKLBCDEFGHIJK 1 A_Chase("", "Missile", CHF_DONTMOVE)
		Loop
	Missile:
		LSEN A 0 A_PlaySound("lasersentryfire")
		LSEN BCDEFGHIJKL 1 A_CustomRailgun(random(1,2) , 0 , none , "white", 1, 1, 500)
		LSEN L 0 A_ClearTarget
		Goto See
	Death:
		LSEN A 0 A_Scream
		LSEN A 0 A_Fall
		LSEN M 1 A_SpawnItem("LightningSentryDeathFX",0,110)
		LSEN M 200
		Goto Fadeout
	Fadeout:
		LSEN M 1 A_FadeOut(0.1)
		Loop
	}
}

// --->
// Search & Destroy Orb
// --->
Actor SDOrb
{
	MONSTER
	Radius 31
	Height 56
	Scale 0.4
	Speed 5
	Health 400
	Mass 750
	obituary "%o was found by a search & destroy orb." 
	floatspeed 8
	+NOBLOOD
	+FLOAT
	+NOGRAVITY
	+DONTHARMSPECIES
	seesound "sdorbsee"
	deathsound "sdorbdeath"
	activesound "sdorbact"
	renderstyle translucent
	alpha 1.0
	Species "Machines"
	DropItem "ArmorCharge" 60
	DropItem "NewCell" 80 20
	States
	{
	Spawn:
		ROBS A 1 A_Look
		Loop
	See:
		ROBS AABBCCDDEEFF 2 A_Chase
		Loop
	Missile:
		ROBS A 0 A_Stop
		ROBS A 4 A_FaceTarget
		ROBS A 6 A_CustomMissile("sdorbplasma", 70,0,0)
		ROBS A 1 A_FaceTarget
		ROBS A 6 A_CustomMissile("sdorbplasma", 70,0,0)
		ROBS A 1 A_FaceTarget
		ROBS A 6 A_CustomMissile("sdorbplasma", 70,0,0)
		ROBS A 1 A_FaceTarget
		Goto See
	Death:
		ROBS A 3 A_Scream
		ROBS G 4 A_Fall
		ROBS HIJK 5 A_FadeOut(0.2)
		Stop
	}
}

// --->
// Extra Data & Projectiles
// --->
// Used by Lightning Sentry
actor LightningSentryDeathFX
{
	-SOLID
	+NOGRAVITY
	States
	{
	Spawn:
		LSEN NOPQR 2
		Stop
	}
}

actor LightningSentryDecoy 12360
{
	-SOLID
	States
	{
	Spawn:
		LSEN A 1
		Loop
	}
}

// Used by Search & Destroy Orb
actor sdorbplasma : PlasmaBall
{
	Scale 0.5
	speed 15
	damage 1
	SeeSound "sdorbfire"
	translation "192:207=168:191"
	+RIPPER
	States
	{
	Spawn:
		PLSS AB 1 bright A_SpawnItemEx("sdorbplasmafx", 0, 0, 0, 0, 0, 0, 0, 128, 0)
		loop
	}
}

actor sdorbplasmafx
{
	-SOLID
	+NOGRAVITY
	Scale 0.5
	translation "192:207=168:191"
	renderstyle add
	alpha 0.7
	States
	{
	Spawn:
	PLSE EDCBA 2 bright A_FadeOut(0.1)
	Stop
	}
}

// --->
// Invasion Spawn Spots
// --->
actor SCrusaderSpot : CustomMonsterInvasionSpot 12369
{DropItem Crusader~}

actor SReaverSpot : CustomMonsterInvasionSpot 12370
{DropItem Reaver~}

actor SInquisitorSpot : CustomMonsterInvasionSpot 12371
{DropItem Inquisitor~}

actor sdorbSentrySpot : CustomMonsterInvasionSpot 12362
{DropItem SDOrb}

actor LightningSentrySpot : CustomMonsterInvasionSpot 12361
{DropItem LightningSentry}