//Decorate data base of some doom variant monsters.
//Alpha status: Complete.
//Todo: Remove the redundant monsters and create a data section.

// --->
// SandDemon | Note: Very likely to be removed.
// --->

actor SandDemon : BullFiend
{
	Translation "16:47=48:79"
	damagefactor "Rock", 0.0
	speed 12
	Obituary "%o was gored by a sand demon."
	HitObituary "%o was mangled by a sand demon." 
	States 
	{ 
	Spawn: 
		SARG AB 12 A_Look
		Loop
	See: 
		SARG AABB 2 A_Chase
		TNT1 A 0 A_SpawnItem("HeavyFleshStep",0,0,0,0)
		SARG CCDD 2 A_Chase
		TNT1 A 0 A_SpawnItem("HeavyFleshStep",0,0,0,0)
		Loop 
	Melee: 
		SARG EF 5 A_FaceTarget
		SARG G 4 A_MeleeAttack
		SARG E 5	A_FaceTarget
		Goto See
	Missile:
		TNT1 A 0 A_JumpIfCloser(200,1)
		Goto See
		TNT1 A 0 A_PlaySound("demon/melee")
		SARG EF 7 A_FaceTarget
		TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,1)
		TNT1 A 0 ThrustThingZ(0,20,0,1)
		SARG F 2 A_Recoil(-24)
		SARG F 3 A_CustomMissile("PinkyLunge",18,0,0,CMF_AIMDIRECTION)
		SARG G 6
		SARG G 2 A_ScaleVelocity(0.2)
		TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
		SARG G 1 A_ScaleVelocity(0)
		Goto See
	Pain: 
		TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
		SARG H 4 A_Pain
		Goto See
	XDeath:
	Death: 
		TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
		SARG I 5 
		SARG J 5 A_Scream
		SARG K 4
		SARG L 4 A_NoBlocking     
		SARG M 4 A_SpawnItem("BodyCrash",0,0,0,0)
		SARG N -1 
		Stop
	Raise: 
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("VileHealEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		SARG NMLKJI 5 
		Goto See 
	}
}

// --->
// Icedemon | Note: Very likely to be removed.
// --->

ACTOR Icedemon : BullFiend
{
	Translation "16:47=194:207"
	Obituary "%o became an ice statue by an ice demon."
	HitObituary "%o was mangled by a ice demon."
	States 
	{ 
	Spawn: 
		SARG AB 12 A_Look
		Loop
	See: 
		SARG AABB 2 A_Chase
		TNT1 A 0 A_SpawnItem("HeavyFleshStep",0,0,0,0)
		SARG CCDD 2 A_Chase
		TNT1 A 0 A_SpawnItem("HeavyFleshStep",0,0,0,0)
		Loop 
	Melee: 
		SARG EF 5 A_FaceTarget
		SARG G 4 A_MeleeAttack
		SARG E 5 A_FaceTarget
		Goto See
	Missile:
		TNT1 A 0 A_JumpIfCloser(200,5)
		SARG E 5 A_FaceTarget
		SARG E 0 A_PlaySound(IceGuyAttack)
		SARG F 5 A_CustomMissile("IceGuyFX", 28, 0, 0)
		SARG G 5 
		Goto See
		TNT1 A 0 A_PlaySound("demon/melee")
		SARG EF 7 A_FaceTarget
		TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,1)
		TNT1 A 0 ThrustThingZ(0,20,0,1)
		SARG F 2 A_Recoil(-24)
		SARG F 3 A_CustomMissile("PinkyLunge",18,0,0,CMF_AIMDIRECTION)
		SARG G 6
		SARG G 2 A_ScaleVelocity(0.2)
		TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
		SARG G 1 A_ScaleVelocity(0)
		Goto See
	Pain: 
		TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
		SARG H 4 A_Pain
		Goto See
	XDeath:
	Death: 
		TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
		SARG I 5 
		SARG J 5 A_Scream
		SARG K 4
		SARG L 4 A_NoBlocking     
		SARG M 4 A_SpawnItem("BodyCrash",0,0,0,0)
		SARG N -1 
		Stop
	Raise: 
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("VileHealEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		SARG NMLKJI 5 
		Goto See 
		}
}

// --->
// Catharsi | could be added togheter with the imps.
// --->
ACTOR Catharsi 29123
{
  OBITUARY "%o was blasted by a catharsi."
  Health 120
  Radius 20
  Height 56
  Mass 100
  Speed 8
  PainChance 140
  SEESOUND "catharsi/sit"
  PAINSOUND "catharsi/pain"
  DEATHSOUND "catharsi/death"
  ACTIVESOUND "imp/active"
  MONSTER
  +FloorClip
  states
  {
  Spawn:
    SDDM AB 10 A_Look
    Loop
  See:
    SDDM AABBCCDD 3 A_Chase
    Loop
  Missile:
    SDDM E 0 A_Jump(128, 15)
    SDDM E 4
    SDDM E 10 A_FaceTarget
    SDDM F 2 BRIGHT A_CustomMissile("CatharsiBall", 30, 8, -4, 0)
    SDDM E 2
    SDDM F 2 BRIGHT A_CustomMissile("CatharsiBall", 30, 8, 0, 0)
    SDDM E 2
    SDDM F 2 BRIGHT A_CustomMissile("CatharsiBall", 30, 8, 4, 0)
    SDDM E 2      
    SDDM F 2 BRIGHT A_CustomMissile("CatharsiBall", 30, 8, 8, 0)
    SDDM E 2
    SDDM F 2 BRIGHT A_CustomMissile("CatharsiBall", 30, 8, 12, 0)
    SDDM E 2
    SDDM F 2 BRIGHT A_CustomMissile("CatharsiBall", 30, 8, 16, 0)
    SDDM E 2
    Goto See 
    SDDM E 4
    SDDM E 10 A_FaceTarget
    SDDM F 2 BRIGHT A_CustomMissile("CatharsiBall", 30, 8, 4, 0)
    SDDM E 2
    SDDM F 2 BRIGHT A_CustomMissile("CatharsiBall", 30, 8, 0, 0)
    SDDM E 2
    SDDM F 2 BRIGHT A_CustomMissile("CatharsiBall", 30, 8, -4, 0)
    SDDM E 2      
    SDDM F 2 BRIGHT A_CustomMissile("CatharsiBall", 30, 8, -8, 0)
    SDDM E 2
    SDDM F 2 BRIGHT A_CustomMissile("CatharsiBall", 30, 8, -12, 0)
    SDDM E 2
    SDDM F 2 BRIGHT A_CustomMissile("CatharsiBall", 30, 8, -16, 0)
    SDDM E 2
    Goto See
  Pain:
    SDDM G 3
    SDDM G 3 A_Pain
    Goto See
  Death:
    SDDM H 5
    SDDM I 5 A_Scream
    SDDM J 5 A_NoBlocking  
    SDDM J 0 A_SpawnItem("CatharsiDetonator", 0, 30)
    SDDM KLM 5
    SDDM N -1
    Stop
  XDeath:
    SDDM O 5
    SDDM P 5 A_XScream
    SDDM Q 5 A_NoBlocking
    SDDM RSTUV 5
    SDDM W -1
    Stop
  Raise:
    SDDM NMLKJIH 5
    Goto See
  }
}

ACTOR CatharsiBall
{
  Radius 6
  Height 8
  Speed 15
  Damage 3
  RENDERSTYLE Add
  SEESOUND "catharsi/shotsit"
  DEATHSOUND "catharsi/shotdth"
  PROJECTILE
  +Randomize
  states
  {
  Spawn:
    SDFB A 4 BRIGHT
    Loop
  Death:
    SDFB BCDE 6 BRIGHT
    stop
  }
}

ACTOR CatharsiDetonator
{
  Radius 6
  Height 8
  Speed 0
  SEESOUND "catharsi/shotsit"
  DEATHSOUND "catharsi/shotdth"
  Obituary "%o couldn't evade the Catharsi's detonator."
  MONSTER
  +FloatBob
  +Randomize
  +NoGravity    
  +NoBlood
  states
  {
  Spawn:
    THRM A 4 A_PlaySound("catharsi/thermtick")
    THRM B 16 
    THRM A 4 A_PlaySound("catharsi/thermtick")
    THRM B 16
    THRM A 4 A_PlaySound("catharsi/thermtick")
    THRM B 16
    THRM A 4 A_PlaySound("catharsi/thermtick")  
    THRM B 16
    THRM A 4 A_PlaySound("catharsi/thermtick")  
    THRM B 16  
    SDFB B 0 A_CustomMissile("CatharsiBall", 4, 2, 0)
    SDFB B 0 A_CustomMissile("CatharsiBall", 4, 2, 20)
    SDFB B 0 A_CustomMissile("CatharsiBall", 4, 2, 40)
    SDFB B 0 A_CustomMissile("CatharsiBall", 4, 2, 60)
    SDFB B 0 A_CustomMissile("CatharsiBall", 4, 2, 80)
    SDFB B 0 A_CustomMissile("CatharsiBall", 4, 2, 100)
    SDFB B 0 A_CustomMissile("CatharsiBall", 4, 2, 120)
    SDFB B 0 A_CustomMissile("CatharsiBall", 4, 2, 140)
    SDFB B 0 A_CustomMissile("CatharsiBall", 4, 2, 160)
    SDFB B 0 A_CustomMissile("CatharsiBall", 4, 2, 180)
    SDFB B 0 A_CustomMissile("CatharsiBall", 4, 2, 200)
    SDFB B 0 A_CustomMissile("CatharsiBall", 4, 2, 220)
    SDFB B 0 A_CustomMissile("CatharsiBall", 4, 2, 240)
    SDFB B 0 A_CustomMissile("CatharsiBall", 4, 2, 260)
    SDFB B 0 A_CustomMissile("CatharsiBall", 4, 2, 280)
    SDFB B 0 A_CustomMissile("CatharsiBall", 4, 2, 300)
    SDFB B 0 A_CustomMissile("CatharsiBall", 4, 2, 320)
    SDFB B 0 A_CustomMissile("CatharsiBall", 4, 2, 340)
    SDFB BCDE 6 BRIGHT A_Die
    Stop
  Death:
    NULL A 0
    Stop
  }
}


// --->
// CacoLich | c: Imported directly from Randomons.
// --->

ACTOR CacoLich : CacoDemon~
{
//RD cacolich 700, Arm 500
	Health 700
//	Speed 12
	PainChance 90
	BloodType "GreenBlood"
	BloodColor "00 ff 00"
	DropItem "LifeEssence" 100
	DropItem "ArmorBonusMax" 100
	SeeSound "CacoLich/Sight"
	ActiveSound "CacoLich/Active"
	PainSound "CacoLich/Pain"
	DeathSound "CacoLich/Death"
	Obituary "%o was blasted by the Cacolich"
	HitObituary "%o was munched on by a Cacolich." 
	+QUICKTORETALIATE
	States
	{
	Spawn:
		CLIH A 1 A_Look
		Loop
	See:
		CLIH A 3 A_Chase
		TNT1 A 0 A_Jump(6,"Evade")
		Loop
	Evade:
		TNT1 A 0 A_Jump(128,7)
		TNT1 A 0 ThrustThing(Angle*256/360+64,14,0,0)
		CLIH AAAA 3 A_Chase
		TNT1 A 0 A_Stop
		Goto See
		TNT1 A 0 ThrustThing(Angle*256/360+192,14,0,0)
		CLIH AAAA 3 A_Chase
		TNT1 A 0 A_Stop
		Goto See
	Missile:
		TNT1 A 0 A_Jump(128,"Comet","Seeking")
		CLIH EF 6 A_FaceTarget
		CLIH G 0 Bright A_CustomMissile("WatcherBall", 24, 0, 0)
		CLIH G 0 Bright A_CustomMissile("WatcherBall", 24, 0, 4)
		CLIH G 6 Bright A_CustomMissile("WatcherBall", 24, 0, -4)
		CLIH G 6 A_FaceTarget
		CLIH G 0
		Goto See
	Seeking:
		CLIH B 6 A_Facetarget
		TNT1 A 0 A_CustomMissile("CacoLichBall2",35,0,16,0,0)
		CLIH C 4 Bright A_CustomMissile("CacoLichBall2",35,0,-16,0,0)
		CLIH CD 4	
		TNT1 A 0 A_Jump(87,"Comet")
		Goto See
	Comet:
		CLIH EF 6 A_Facetarget
		CLIH G 5 Bright A_CustomMissile("LichComet",24,0,0,0,0)
		CLIH GG 4
		Goto See
	Melee:
		CLIH EF 4 Bright A_FaceTarget 
		TNT1 A 0 Bright A_PlaySound("monster/psoattack")
		CLIH G 3 Bright A_CustomMissile("LichBreath", 10, 0, Random(18,-18))
		TNT1 A 0 Bright A_FaceTarget
		CLIH F 3 Bright A_CustomMissile("LichBreath", 10, 0, Random(18,-18))
		TNT1 A 0 Bright A_FaceTarget
		CLIH G 3 Bright A_CustomMissile("LichBreath", 10, 0, Random(18,-18))
		TNT1 A 0 Bright A_FaceTarget
		CLIH F 3 Bright A_CustomMissile("LichBreath", 10, 0, Random(18,-18))
		TNT1 A 0 Bright A_FaceTarget
		CLIH G 3 Bright A_CustomMissile("LichBreath", 10, 0, Random(18,-18))
		TNT1 A 0 Bright A_FaceTarget
		CLIH F 3 Bright A_CustomMissile("LichBreath", 10, 0, Random(18,-18))
		TNT1 A 0 Bright A_FaceTarget
		CLIH G 3 Bright A_CustomMissile("LichBreath", 10, 0, Random(18,-18))
		TNT1 A 0 Bright A_FaceTarget
		CLIH F 3 Bright A_CustomMissile("LichBreath", 10, 0, Random(18,-18))
		TNT1 A 0 Bright A_FaceTarget
		CLIH G 3 Bright A_CustomMissile("LichBreath", 10, 0, Random(18,-18))
		TNT1 A 0 Bright A_FaceTarget
		CLIH F 3 Bright A_CustomMissile("LichBreath", 10, 0, Random(18,-18))
		CLIH FG 3
		Goto See
	Pain:
		CLIH H 3 
		CLIH I 6 A_Pain
		TNT1 A 0 A_Jump(70,"Missile")
		Goto See
	XDeath:
	Death:
		CLIH J 0
		CLIH J 0 A_SetFloorClip
		CLIH J 6 A_Scream
		CLIH KLMN 6
		Wait
	Crash:
		CLIH OP 6 A_SpawnItemEx("GreenGibbedGenerator",0,0,0,0,0,0,0,128,0)
		CLIH Q 6 A_NoBlocking
		CLIH R 6 
		CLIH S -1
		Stop
	}
}

ACTOR CacoLichBall2
{
	Radius 8
	Height 6
	Speed 20
	Damage 7
	PROJECTILE
	+SEEKERMISSILE
	RENDERSTYLE ADD
	ALPHA 0.8
	SeeSound "Imp/attack"
	DeathSound "Imp/shotx"
	Decal CacoScorch
	States
	{
	Spawn:
		CB4L AB 4 Bright BRIGHT A_SeekerMissile(5,15)
		TNT1 A 0 BRIGHT A_SpawnItemEx("WatcherBallTrail",0,0,2,0,0,0,0,128,0)
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx("GreenParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
		TNT1 AAA 0 A_CustomMissile("Kaboom7", 0, 0, random(0,360), 2, random(0,360))
		TNT1 AAA 0 A_CustomMissile("SmokeFX4", 0, 0, random(0,360), 2, random(0,360))	
		CB4L CDEFG 6 BRIGHT
		Stop
	}
}

ACTOR LichBreath : PoisonSoulCloud
{DamageType "CacoComet"}

ACTOR LichComet : Comet
{DamageType "CacoComet"}

// --->
// Agathodemon | c: Eriance, Vader, Jimmy91
// Note: Soon will be replaced by realm667 one with brightmaps too.
// So it'll be paired whit the original complex bestiary.
// --->

actor Agathodemon : Cacodemon~
{
//CD caco 600, Arm 650
Health 750
//CD caco 6
Speed 12
PainChance 100
BloodType "NewBlood"
BloodColor "ff 00 00"
Obituary "%o was desintegrated by an agathodemon."
HitObituary "%o got caught in an agathodemon's jaws."
DropItem "LifeEssence" 115
DropItem "ArmorBonusMax" 115
+MISSILEMORE
States
{
	Spawn:
		AGAH A 1 A_Look
		Loop
	See:
		AGAH A 3 A_Chase
		TNT1 A 0 A_Jump(4,"Evade")		 
		Loop
	Evade:
		TNT1 A 0 A_JumpIfTargetInLOS(1)
		Goto See
		TNT1 A 0 A_Jump(128,7)
		TNT1 A 0 ThrustThing(Angle*256/360+64,14,0,0)
		AGAH AAAA 2 A_Chase
		TNT1 A 0 A_Stop
		Goto See
		TNT1 A 0 ThrustThing(Angle*256/360+192,14,0,0)		 
		AGAH AAAA 2 A_Chase
		TNT1 A 0 A_Stop		 
		Goto See
	Missile:
		TNT1 A 0 A_JumpIfCloser(240,"Charge")
		TNT1 A 0 A_Jump(52,"Stealth")
		TNT1 A 0 A_Jump(128,"Fireball")
	Tracer:
		AGAH BC 4 A_FaceTarget
		AGAH D 0 A_CustomMissile("AgathoDemonTracer",22,0,-30)
		AGAH D 5 A_CustomMissile("AgathoDemonTracer",22,0,30)
		AGAH D 0 A_Jump(192, "See")
	Fireball:
		AGAH BC 4 A_FaceTarget
		AGAH D 5 Bright A_CustomMissile("AgathoDemonFireBall",24)
		AGAH D 0 A_Jump(192, "See")
		Goto Tracer
	Stealth:
		AGAH D 0 A_JumpIfInventory("AgathodemonStealthCheck",1,16)
		AGAH BC 5 Bright A_FaceTarget
		AGAH D 2 Bright A_PlaySound("agatho/stealth")
		AGAH D 0 A_ChangeFlag("NOPAIN", 0)
		AGAH DDDDDDDD 2 Bright A_FadeOut(0.1)
		AGAH D 0 A_ChangeFlag("NOPAIN", 1)
		AGAH D 0 A_ChangeFlag("MISSILEMORE", 0)
		AGAH D 0 A_GiveInventory("AgathodemonStealthCheck",1)
		Goto See
		AGAH BC 5 Bright A_FaceTarget
		AGAH D 2 Bright A_PlaySound("agatho/stealth")
		AGAH D 0 A_ChangeFlag("NOPAIN", 0)
		AGAH DDDDDDDD 2 Bright A_FadeIn(0.1)
		AGAH D 0 A_ChangeFlag("NOPAIN", 1)
		AGAH D 0 A_ChangeFlag("MISSILEMORE", 1)
		AGAH A 0 A_TakeInventory("AgathodemonStealthCheck",1)
		Goto See
	Charge:
		TNT1 A 0 A_Jump(142,2)
		TNT1 A 0 A_Jump(128,"Fireball")
	Goto Tracer
		TNT1 A 0 A_ScaleVelocity(0)
		TNT1 A 0 A_PlaySound("caco/sight")	
		AGAH BC 7 A_FaceTarget
		TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,1)
		AGAH DDD 2 A_Recoil(-8)
		AGAH D 2
		TNT1 A 0 A_PlaySound("caco/melee")
		AGAH C 4 A_CustomMeleeAttack(Random(4,10)*5,"","","CacoBite")
		AGAH B 2 A_ScaleVelocity(0.5)
		AGAH B 2 A_ScaleVelocity(0.25)
		TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
		AGAH A 2 A_ScaleVelocity(0)
		AGAH A 2
		Goto See	
	Melee:
		AGAH BC 5 A_FaceTarget
		AGAH D 5 A_MeleeAttack
		AGAH CB 5 A_FaceTarget
		Goto See
	Pain:
		TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
		AGAH E 12 A_Pain
		Goto See
	XDeath:
	Death:
		TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
		AGAH G 6 A_Scream
		AGAH H 6 A_NoBlocking
	FallLoop:
		TNT1 A 0 A_ChangeFlag(THRUACTORS,1)
		AGAH I 1 A_CheckFloor("Splat")
		Loop
	Splat:
		TNT1 A 0
		TNT1 A 0 A_ChangeFlag(THRUACTORS,0)
		TNT1 AA 0 A_CustomMissile("BlueBloodCloud",FRandom(25,50),0,FRandom(0,360),2,FRandom(0,360))
		TNT1 A 0 A_SetTranslucent(1.0,0)
		AGAH J 6 A_PlaySound("Gore/Splat")
		AGAH K 4
		AGAH L -1 A_SetFloorClip
		Stop
	Raise:
		TNT1 A 0
		TNT1 A 0 A_UnSetFloorClip
		TNT1 A 0 A_SpawnItemEx("VileHealEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		AGAH KJIHG 4
		Goto See
	}
}

actor AgathodemonStealthCheck : Inventory
{
  Inventory.Amount 1
  Inventory.MaxAmount 1
}

actor AgathoDemonFireBall : DoomImpBall //trail projectile
{
  Speed 20
  Damage 9
  Decal DoomImpScorch
  States
  {
  Spawn:
    TNT1 A 2 Bright A_SpawnItem("AgathoDemonFX")
    Loop
  Death:
    AGFX GHIJKL 3 Bright
    Stop
  }
}

actor AgathoDemonFX //trail effect
{
  Scale 0.75
  RenderStyle Add
  +NOGRAVITY
  States
  {
  Spawn:
    AGFX ABCDEF 2 Bright
    Stop
  }
}

actor AgathoDemonTracer : DoomImpBall //homing fireball
{
  +SEEKERMISSILE
  Speed 15
  Damage 4
  Decal BaronScorch
  States
  {
  Spawn:
    AGFB ABCABCABCABCABCABCABCABCABCABCABCABC 4 Bright A_Tracer2
  Death:
    AGFB DEFG 4 Bright
    Stop
  }
}

actor AgathoDemonTracer2 : AgathoDemonTracer
{
scale 2.0
speed 35
damage 7
Radius 12
SeeSound "lasersentrydeath"
Translation "171:191=161:167", "247:247=167:167", "47:47=167:167", "6:6=167:167"
Height 16
 States
  {
  Spawn:
    AGFB ABCABCABCABCABCABCABCABCABCABCABCABC 4 Bright
  Death:
    AGFB DEFG 4 Bright
    Stop
  }
}

actor AgathoDemonTracer3 : AgathoDemonTracer
{
scale 2.0
speed 35
damage 7
Radius 12
SeeSound "lasersentrydeath"
Translation "171:191=192:207", "247:247=207:207", "47:47=207:207", "6:6=207:207"
Height 16
 States
  {
  Spawn:
    AGFB ABCABCABCABCABCABCABCABCABCABCABCABC 4 Bright
  Death:
    AGFB DEFG 4 Bright
    Stop
  }
}

actor AgathoDemonTracer4 : AgathoDemonTracer
{
scale 2.0
speed 35
damage 7
Radius 12
SeeSound "lasersentrydeath"
Height 16
 States
  {
  Spawn:
    AGFB ABCABCABCABCABCABCABCABCABCABCABCABC 4 Bright
  Death:
    AGFB DEFG 4 Bright
    Stop
  }
}

// --->
// Illus
// --->
Actor Illus
{
	Health 300
	Radius 25
	Height 65
	Mass 250
	Scale 1.2
	Speed 10
	Monster
	renderstyle translucent
	alpha 0.8
	meleedamage 12
	+FLOORCLIP
	SeeSound "Illus/Sight"
	DeathSound "Illus/Death"
	ActiveSound "Illus/Active"
	MeleeSound "Illus/Melee"
	Obituary "%o was clawed inside by an illus."
	BloodType "BlackBlood"
	BloodColor "30 30 30"
	DropItem "LifeEssence" 40
	DropItem "ArmorBonusMax" 40
	States
	{
	Spawn:
		ILLS AB 10 bright A_Look
		Loop
	See:
		ILLS A 3 bright A_Chase
		ILLS A 0 bright A_SpawnItemEx("IllusImageA", 0, 0, 0, 0, 0, 0, 0, 128)
		ILLS A 3 bright A_Chase
		ILLS A 0 bright A_SpawnItemEx("IllusImageA", 0, 0, 0, 0, 0, 0, 0, 128)
		ILLS B 3 bright A_Chase
		ILLS B 0 bright A_SpawnItemEx("IllusImageB", 0, 0, 0, 0, 0, 0, 0, 128)
		ILLS B 3 bright A_Chase
		ILLS B 0 bright A_SpawnItemEx("IllusImageB", 0, 0, 0, 0, 0, 0, 0, 128)
		ILLS C 3 bright A_Chase
		ILLS C 0 bright A_SpawnItemEx("IllusImageC", 0, 0, 0, 0, 0, 0, 0, 128)
		ILLS C 3 bright A_Chase
		ILLS C 0 bright A_SpawnItemEx("IllusImageC", 0, 0, 0, 0, 0, 0, 0, 128)
		ILLS D 3 bright A_Chase
		ILLS D 0 bright A_SpawnItemEx("IllusImageD", 0, 0, 0, 0, 0, 0, 0, 128)
		ILLS D 3 bright A_Chase
		ILLS D 0 bright A_SpawnItemEx("IllusImageD", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
	Melee:
		ILLS EFG 5 bright A_FaceTarget
		ILLS H 6 bright A_MeleeAttack
		Goto See
	Missile:
		ILLS E 0 bright A_Jump(128, 6)
		ILLS E 0 bright A_JumpIfCloser(512, 1)
		Goto See
		ILLS EFG 5 bright A_FaceTarget
		ILLS H 6 bright A_SpawnItem("IllusGhost")
		Goto See
		ILLS E 0 bright A_Jump(92, 10)
		ILLS EFG 5 bright A_FaceTarget
		ILLS H 0 bright A_CustomMissile("IllusFakeBlast", 38, 0, Random(-8, 8))
		ILLS H 0 bright A_CustomMissile("IllusFakeBlast", 38, 0, Random(-8, 8))
		ILLS H 0 bright A_CustomMissile("IllusFakeBlast", 38, 0, Random(-8, 8))
		ILLS H 0 bright A_CustomMissile("IllusFakeBlast", 38, 0, Random(-8, 8))
		ILLS H 0 bright A_CustomMissile("IllusBlast", 38, 0, Random(-8, 8))
		ILLS H 6 bright A_CustomMissile("IllusBlast", 38, 0, Random(-8, 8))
		Goto See
		ILLS E 0 bright A_JumpIfCloser(256, 12)
		ILLS EFG 5 bright A_FaceTarget
		ILLS G 0 bright A_FaceTarget
		ILLS G 0 bright A_CustomMissile("IllusShieldSpawner", 38, 0, 0, 2)
		ILLS G 0 bright A_CustomMissile("IllusShieldSpawner", 38, 0, 10, 2)
		ILLS G 0 bright A_CustomMissile("IllusShieldSpawner", 38, 0, -10, 2)
		ILLS G 0 bright A_CustomMissile("IllusShieldSpawner", 38, 0, 20, 2)
		ILLS G 0 bright A_CustomMissile("IllusShieldSpawner", 38, 0, -20, 2)
		ILLS G 0 bright A_CustomMissile("IllusShieldSpawner", 38, 0, 30, 2)
		ILLS G 6 bright A_CustomMissile("IllusShieldSpawner", 38, 0, -30, 2)
		ILLS G 0 bright
		Goto See
	Death:
		ILLS I 0 bright A_Scream
		ILLS I 0 bright A_NoBlocking
		ILLS I 3 bright A_FadeOut(0.02)
		Goto Death+2
	}
}

Actor IllusGhost
{
	Radius 25
	Height 65
	Mass 250
	Scale 1.2
	Speed 30
	Obituary "%o was clawed inside by an illus."
	renderstyle translucent
	alpha 0.4
	meleedamage 12
	meleesound "Illus/Melee"
	ReactionTime 50
	+NOTELEPORT
	+FLOORCLIP
	-SOLID
	-SHOOTABLE
	+NONSHOOTABLE
	+LOOKALLAROUND
	+DROPOFF
	+NOCLIP
	States
	{
	Spawn:
		TNT1 AAAAAAA 1 A_Look
		Goto Death
	See:
		TNT1 AAAAAAAAAA 0 A_Chase
		TNT1 A 1 A_Countdown
		Loop
	Melee:
		ILLS EFG 2 bright A_FaceTarget
		ILLS H 4 bright A_MeleeAttack
	Death:
		TNT1 A 1
		Stop
	}
}

Actor IllusImageA
{
	Radius 4
	Height 8
	scale 1.2
	Speed 0
	Damage 0
	Mass 75
	RenderStyle "Translucent"
	Alpha 0.3
	PROJECTILE
	States
	{
	Spawn:
		ILLS A 10 bright
		Stop
	}
}

Actor IllusImageB
{
	Radius 4
	Height 8
	scale 1.2
	Speed 0
	Damage 0
	Mass 75
	RenderStyle "Translucent"
	Alpha 0.3
	PROJECTILE
	States
	{
	Spawn:
		ILLS B 10 bright
		Stop
	}
}

Actor IllusImageC
{
	Radius 4
	Height 8
	scale 1.2
	Speed 0
	Damage 0
	Mass 75
	RenderStyle "Translucent"
	Alpha 0.3
	PROJECTILE
	States
	{
	Spawn:
		ILLS C 10 bright
		Stop
	}
}

Actor IllusImageD
{
	Radius 4
	Height 8
	scale 1.2
	Speed 0
	Damage 0
	Mass 75
	RenderStyle "Translucent"
	Alpha 0.3
	PROJECTILE
	States
	{
	Spawn:
		ILLS D 10 bright
		Stop
	}
}

Actor IllusShieldSpawner
{
	radius 0
	height 0
	damage 0
	speed 15
	+NOCLIP
	+DROPOFF
	+FLOORCLIP
	+NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 A 10
	Death:
		TNT1 A 0 A_PlaySound("Illus/Shield")
		TNT1 A 1 A_SpawnItem("IllusShield")
	Stop
	}
}

Actor IllusShield
{
	renderstyle translucent
	alpha 0.3
	radius 15
	height 70
	mass 1000000
	+SHOOTABLE
	+NOGRAVITY
	+NOTELEPORT
	+NODAMAGE
	+NOBLOOD
	+GHOST
	states
	{
	Spawn:
		ILSS AAAAAAAAAA 35 bright
	Death:
		ILSS A 3 bright A_FadeOut(0.05)
		Loop
	}
}

Actor IllusBlast
{
	Radius 5
	Height 5
	Speed 25
	scale 0.2
	Damage (30)
	Projectile
	+RANDOMIZE
	+THRUGHOST
	RenderStyle Translucent
	Alpha 0.7
	SeeSound "Illus/BlastFire"
	DeathSound "Illus/BlastHit"
	States
	{
	Spawn:
		ILSB ABCDEFGH 3 bright
		Loop
	Death:
		ILSB AIJKLM 3 bright
		Stop
	}
}

Actor IllusFakeBlast
{
	Radius 5
	Height 5
	Speed 25
	scale 0.2
	Damage (0)
	Projectile
	+RANDOMIZE
	+THRUGHOST
	RenderStyle Translucent
	Alpha 0.7
	SeeSound "Illus/BlastFire"
	States
	{
	Spawn:
		ILSB ABCDEFGH 3 bright
		Loop
	Death:
		TNT1 A 1
		Stop
	}
}

// --->
// Invasion Spawn Spots
// --->
actor SandDemonSpot : CustomMonsterInvasionSpot 12351
{DropItem SandDemon}

actor IcedemonSpot : CustomMonsterInvasionSpot 12367
{DropItem Icedemon}

actor CacolichSpot : CustomMonsterInvasionSpot 15071
{DropItem Cacolich}

actor AgathoSpot : CustomMonsterInvasionSpot 12341
{DropItem AgathoDemon}

actor IllusSpot : CustomMonsterInvasionSpot 15102
{DropItem Illus}
