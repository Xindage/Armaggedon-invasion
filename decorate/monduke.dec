//Decorate data base of monsters based on DukeNuken 3d.
//Alpha status: Complete.

// --->
// PigCop | c: Inuyasha_989
// --->
ACTOR Pigcop 7068
{
	Health 110
	Radius 28
	Height 56
	Speed 13
	Mass 100
	Painchance 140
	DropItem "PortableMedkit" 80
	DropItem "ArmorBooster" 80
	DropItem "AmmoPack" 65
	DropItem "HandGrenadeAmmo" 150 1
	DropItem "MineAmmo" 75 1
	DropItem "Combat Shotgun" 256 1
	MONSTER 
	+FLOORCLIP
	SeeSound "pigcop/sight"
	PainSound "pigcop/pain"
	DeathSound "pigcop/death"
	ActiveSound "pigcop/active"
	Obituary "%o got porked by the L.A.R.D.!"
	Scale 0.8
	States
	{
	Spawn:
		PIGC A 1 A_Look
		Loop
	See:
		PIGC A 4
		PIGC AABBCCDD 4 A_Chase
		Goto See+1
	Missile:
		PIGC E 8 A_FaceTarget
		PIGC E 0 A_PlaySound("pigcop/attack")
		PIGC F 15 BRIGHT A_CustomBulletAttack(13,3,6,3,"bulletpuff")
		PIGC G 17
		PIGC E 8 A_FaceTarget
		PIGC E 0 A_PlaySound("pigcop/attack")
		PIGC F 15 BRIGHT A_CustomBulletAttack(13,3,6,3,"bulletpuff")
		PIGC G 17
		Goto See+1
	Pain:
		PIGC H 2
		PIGC H 4 A_Pain
		Goto See+1
	Death:
		PIGC HI 4
		PIGC J 4 A_Scream
		PIGC K 4
		PIGC L 4 A_Fall
		PIGC M -1
	Stop
	}
}

// --->
// Enforcer
// --->
ACTOR Enforcer 7065 
{
	Health 150
	Radius 24
	Height 50
	Speed 12
	Mass 700
	Painchance 50
	DropItem "PortableMedkit" 80
	DropItem "ArmorBooster" 80
	DropItem "AmmoPack" 65
	DropItem "HandGrenadeAmmo" 150 1
	DropItem "MineAmmo" 75 1
	DropItem "BulletMag" 256 10
	MONSTER 
	+FLOORCLIP
	SeeSound "Enforcer/Sight"
	PainSound "Enforcer/Pain"
	DeathSound "Enforcer/Death"
	ActiveSound "Enforcer/Active"
	Obituary "%o gained a few bullet holes from an Enforcer."
	Scale 0.8
	States
	{
	Spawn:
		ENFO A 4 A_Look
		Loop
	See:
		ENFO AABBCCDD 4 A_Chase
		Loop
	Missile:
		ENFO E 5 bright A_FaceTarget
		ENFO EF 4 bright A_CustomBulletAttack (25, 0, 1, 3, "bulletpuff")
		ENFO F 1 bright A_SpidRefire
		goto Missile+1
	Pain:
		ENFO A 1 A_PAIN
		Goto See
	Death:
		ENFO G 6 A_Scream
		ENFO HIJ 6
		ENFO K 6 A_Noblocking
		ENFO L -1 A_SetFloorClip
		Stop
	}
}

// --->
// Octabrain
// --->
ACTOR Octabrain 7067
{
	Health 170
	Radius 20
	Height 60
	Speed 14
	Mass 700
	Painchance 100
	DropItem "LifeEssence" 20
	DropItem "ArmorBonusMax" 20
	MONSTER 
	+FLOAT
	+NOGRAVITY
	SeeSound "Octabrain/Sight"
	PainSound "Octabrain/Pain"
	DeathSound "Octabrain/Death" 
	ActiveSound "Octabrain/Active"
	MeleeSound "Octabrain/Meele"
	Obituary "%o was killed by an Octabrain."
	Hitobituary "%o was eaten alive by an Octabrain."
	Scale 0.8
	Meleedamage 5
	States
	{
	Spawn:
		OCTA A 4 A_Look
		Loop
	See:
		OCTA AABBCC 4 A_Chase
		Loop
	Missile:
		OCTA DDDD 5 A_Facetarget
		OCTA D 0 A_PLAYSOUND ("Octabrain/Attack")
		OCTA E 12 Bright A_CustomMissile("OCTOProjectile", 20, 0, 0)
		goto see
	Melee:
		OCTA CB 10 A_facetarget
		OCTA A 10 A_Meleeattack
		Goto see
	Pain:
		OCTA D 4 A_PAIN
		Goto Missile
	Death:
		OCTA F 6 A_Scream
		OCTA GHI 10
		OCTA J 10 A_Noblocking
		OCTA K -1 A_SetFloorClip
		Stop
	}
}

// --->
// Assault Commander
// --->
ACTOR AssaultCommander 7062
{
	Health 400
	Radius 30
	Height 30
	Speed 13
	Mass 1000
	Painchance 30
	DropItem "LifeEssence" 60
	DropItem "ArmorBonusMax" 60
	DropItem "MissileBox" 32 10
	DropItem "Missile" 128
	DropItem "AmmoPack" 32
	MONSTER 
	+FLOAT
	+NOGRAVITY
	+NORADIUSDMG
	+MISSILEMORE
	SeeSound "AssaultCo/Sight"
	PainSound "AssaultCo/Pain"
	DeathSound "AssaultCo/Death" 
	ActiveSound "AssaultCo/Active"
	MeleeSound "AssaultCo/Meele"
	Obituary "%o got blasted to bits by an Assault Commander."
	Scale 0.8
	Meleedamage 2
	States
	{
	Spawn:
		ASCO A 4 A_Look
		Loop
	See:
		ASCO AABBCC 3 A_Chase
		Loop
	Melee:
		ASCO N 6 A_facetarget
		ASCO N 6 A_Meleeattack
		Goto see
	Missile:
		ASCO E 12 A_Facetarget
		ASCO E 0 A_PLAYSOUND ("AssaultCo/Attack")
		ASCO E 6 Bright A_CustomMissile("DukeRocket", 20, 0, 0)
		goto see
	Pain:
		ASCO D 4 A_PAIN
		Goto See
	Death:
		ASCO F 8 A_Scream
		ASCO GHIJKL 8
		ASCO M 8 A_Noblocking
		ASCO M -1 A_SetFloorClip
		Stop
	}
}

// --->
// Battlelord
// --->
ACTOR Battlelord 7069
{
	Health 5000
	Radius 40
	Height 190
	Speed 12
	Mass 1000
	Painchance 7
	DropItem "BossLifeEssence" 128 25
	DropItem "BossArmorBonusMax" 128 1
	DropItem "MissileBox" 192 20
	DropItem "DemonRune", 2
	MONSTER
	+FLOORCLIP
	+DONTMORPH
	+NORADIUSDMG
	+MISSILEMORE
	SeeSound "Battlelord/Sight"
	AttackSound "Battlelord/Attack"
	PainSound "Battlelord/Pain"
	DeathSound "Battlelord/Death" 
	ActiveSound "Battlelord/Active"
	Obituary "%o could not go up against the Battlelord's weapons."
	States
	{
	Spawn:
		LORD A 4 A_Look
		Loop
	See:
		LORD AABBCCDD 4 A_Chase
		Loop
	Missile:
		LORD G 0 A_JUMP (120,11)
		LORD G 12 Bright A_Facetarget
		LORD G 0 A_PLAYSOUND ("BOFIRE")
		LORD H 12 A_ThrowGrenade("Mortar", 20, 20, 10)
		LORD G 12 Bright A_Facetarget
		LORD G 0 A_PLAYSOUND ("BOFIRE")
		LORD H 12 A_ThrowGrenade("Mortar", 20, 20, 10)
		LORD G 12 Bright A_Facetarget
		LORD G 0 A_PLAYSOUND ("BOFIRE")
		LORD H 12 A_ThrowGrenade("Mortar", 20, 20, 10)
		goto see
		LORD E 20 bright A_FaceTarget
		LORD EF 4 bright A_CustomBulletAttack (10, 0, 1, 4, "bulletpuff")
		LORD F 1 bright A_SpidRefire
		goto Missile+11
	Pain:
		LORD I 4 A_PAIN
		LORD I 0 A_JUMP (100,1)
		Goto See
		LORD I 0
		Goto Missile+11
	Death:
		LORD I 10 A_Scream
		LORD IJKL 10
		LORD M 10 A_Noblocking
		LORD N -1 A_SetFloorClip
		Stop
	}
}

// --->
// Overlord | c: Hellraiser and modified by Wario
// --->
ACTOR Overlord 7066
{
	Health 7000
	Radius 60
	Height 110
	Speed 12
	Mass 1000
	Painchance 7
	DropItem "BossLifeEssence" 162 25
	DropItem "BossArmorBonusMax" 162 1
	DropItem "MissileBox" 192 20
	DropItem "DemonRune", 4
	MONSTER
	+FLOORCLIP
	+NORADIUSDMG
	+DONTMORPH
	+NORADIUSDMG
	+MISSILEMORE
	SeeSound "overlord/see"
	DeathSound "overlord/death"
	ActiveSound "overlord/roam"
	PainSound "Overlord/pain"
	Obituary "%o shit %hself looking at the Overlord."
	states
	{
	Spawn:
		DNET A 10 A_Look
		loop
	See:
		DNET A 4 A_Chase
		DNET B 4 A_Chase
		DNET C 4 A_Chase
		DNET D 4 A_Chase
		loop
	Pain:
		DNET A 1 A_PAIN
		goto see
	Death:
		DNET Q 12 A_Scream
		DNET S 12
		DNET T 10
		DNET U 10
		DNET V 10
		DNET X 10
		DNET Y 10
		DNET Z -1 A_NoBlocking
		Stop
	Missile:
		DNET E 10 A_FaceTarget
		DNET F 10 A_FaceTarget
		DNET G 5 A_SpidRefire
		DNET H 5 bright A_CustomMissile("DukeRocket", 95, 15, 0, 0, 0)
		DNET G 5 A_FaceTarget
		DNET H 5 bright A_CustomMissile("DukeRocket", 95, -16, 0, 0, 0)
		goto Missile+2
	}
}

// --->
// Cycloid Emperor
// --->
ACTOR CycloidEmperor 7064 
{
	Health 8000
	Radius 50
	Height 120
	Speed 14
	Mass 7000
	Painchance 5
	DropItem "BossLifeEssence" 192 25
	DropItem "BossArmorBonusMax" 192 1
	DropItem "MissileBox" 162 20
	DropItem "NewCellPack" 162 140
	DropItem "DemonRune", 8
	MONSTER
	+FLOORCLIP
	+NORADIUSDMG
	+DONTMORPH
	+NORADIUSDMG
	+MISSILEMORE
	SeeSound "CYCLSIT"
	PainSound "CYCLPAIN"
	DeathSound "CYCLDIE" 
	ActiveSound "CYCLACT"
	Obituary "%o was devastated by the Cycloid Emperor."
	States
	{
	Spawn:
		CYCL A 4 A_Look
		Loop
	See:
		CYCL ABCD 4 A_Chase
		Loop
	Missile:
		CYCL E 0 A_JUMP(128,32)
		CYCL F 10 A_Facetarget
		CYCL E 0 A_PLAYSOUND ("CYCLATK")
		CYCL E 5 Bright A_CustomMissile("DukeRocket2", 40, 30, 0)
		CYCL E 0 Bright A_CustomMissile("DukeRocket2", 40, 30, 20)
		CYCL F 4 Bright A_Facetarget
		CYCL E 5 Bright A_CustomMissile("DukeRocket2", 40, 30, 10)
		CYCL E 0 Bright A_CustomMissile("DukeRocket2", 40, 30, -40)
		CYCL F 4 Bright A_Facetarget
		CYCL E 5 Bright A_CustomMissile("DukeRocket2", 40, 30, -10)
		CYCL E 0 Bright A_CustomMissile("DukeRocket2", 40, 30, 50)
		CYCL F 4 Bright A_Facetarget
		CYCL E 5 Bright A_CustomMissile("DukeRocket2", 40, 30, -20)
		CYCL E 0 Bright A_CustomMissile("DukeRocket2", 40, 30, 35)
		CYCL F 4 Bright A_Facetarget
		CYCL E 5 Bright A_CustomMissile("DukeRocket2", 40, 30, 0)
		CYCL E 0 Bright A_CustomMissile("DukeRocket2", 40, 30, -15)
		CYCL F 4 Bright A_Facetarget
		CYCL E 5 Bright A_CustomMissile("DukeRocket2", 40, 30, 0)
		CYCL E 0 Bright A_CustomMissile("DukeRocket2", 40, 30, 20)
		CYCL F 4 Bright A_Facetarget
		CYCL E 5 Bright A_CustomMissile("DukeRocket2", 40, 30, 10)
		CYCL E 0 Bright A_CustomMissile("DukeRocket2", 40, 30, -40)
		CYCL F 4 Bright A_Facetarget
		CYCL E 5 Bright A_CustomMissile("DukeRocket2", 40, 30, -10)
		CYCL E 0 Bright A_CustomMissile("DukeRocket2", 40, 30, 50)
		CYCL F 4 Bright A_Facetarget
		CYCL E 5 Bright A_CustomMissile("DukeRocket2", 40, 30, -20)
		CYCL E 0 Bright A_CustomMissile("DukeRocket2", 40, 30, 35)
		CYCL F 4 Bright A_Facetarget
		CYCL E 5 Bright A_CustomMissile("DukeRocket2", 40, 30, 0)
		CYCL E 0 Bright A_CustomMissile("DukeRocket2", 40, 30, -15)
		goto see
		CYCL G 10 A_Facetarget
		CYCL G 0 A_PLAYSOUND ("OCTOATK")
		CYCL H 3 Bright A_CustomMissile("OCTOProjectile2", 80, 0, -20)
		CYCL H 1 Bright A_facetarget
		CYCL H 3 Bright A_CustomMissile("OCTOProjectile2", 80, 0, 10)
		CYCL H 1 Bright A_facetarget
		CYCL H 3 Bright A_CustomMissile("OCTOProjectile2", 80, 0, -30)
		CYCL H 1 Bright A_facetarget
		CYCL H 3 Bright A_CustomMissile("OCTOProjectile2", 80, 0, 0)
		CYCL H 1 Bright A_facetarget
		CYCL H 3 Bright A_CustomMissile("OCTOProjectile2", 80, 0, 25)
		CYCL H 1 Bright A_facetarget
		CYCL H 3 Bright A_CustomMissile("OCTOProjectile2", 80, 0, -50)
		CYCL H 1 Bright A_facetarget
		CYCL H 3 Bright A_CustomMissile("OCTOProjectile2", 80, 0, 30)
		CYCL H 1 Bright A_facetarget
		CYCL H 3 Bright A_CustomMissile("OCTOProjectile2", 80, 0, 10)
		CYCL H 1 Bright A_facetarget
		CYCL H 3 Bright A_CustomMissile("OCTOProjectile2", 80, 0, -35)
		CYCL H 1 Bright A_facetarget
		CYCL H 3 Bright A_CustomMissile("OCTOProjectile2", 80, 0, -15)
		CYCL H 1 Bright A_facetarget
		CYCL H 3 Bright A_CustomMissile("OCTOProjectile2", 80, 0, 0)
		CYCL H 1 Bright A_facetarget
		CYCL H 3 Bright A_CustomMissile("OCTOProjectile2", 80, 0, -20)
		Goto see
	Pain:
		CYCL A 2 A_PAIN
		Goto See
	Death:
		CYCL I 14 A_Scream
		CYCL JKLMNO 14
		CYCL P 14 A_Noblocking
		CYCL Q -1 A_SetFloorClip
		Stop
	}
}

// --->
// Extra Data & Projectiles
// --->
// Used by Octabrain
ACTOR OCTOProjectile 
{
	radius 18
	height 10
	speed 12
	damage 5
	Scale 0.6
	Projectile
	States
	{
	Spawn:
		OCFX ABCDEFGHIJKLMNOPQR 4 bright
		Goto Death
	Death:
		OCFX ST 4 bright 
		Stop
	}
}

// Used by Assault Commander & Overlord
ACTOR DukeRocket
{
	radius 12
	height 8
	speed 20
	damage 10
	scale 0.8
	explosiondamage 16
	Explosionradius 64
	DeathSound "BOEXP"
	Projectile
	States
	{
	Spawn:
		DRPG A 4 
		Loop
	Death:
		DNEX A 3 Bright A_explode
		DNEX BCDEFGHIJKLMNOPQRSTU 3 bright 
		Stop
	}
}

// Used by Battlelord
ACTOR Mortar
{
	radius 6
	height 8
	speed 10
	damage 20
	explosiondamage 64
	Explosionradius 128
	Seesound "OVLOATK"
	DeathSound "BOEXP"
	+NOBLOCKMAP
	+DROPOFF 
	+MISSILE 
	+ACTIVATEIMPACT 
	+ACTIVATEPCROSS 
	+NOTELEPORT 
	States
	{
	Spawn:
		MORT ABCD 4 A_SeekerMissile(90,90)
		Loop
	Death:
		DNEX A 3 Bright A_explode
		DNEX A 0 A_scream
		DNEX BCDEFGHIJKLMNOPQRSTU 3 bright 
		Stop
	}
}

// Used by Cycloid Emperor
ACTOR OCTOProjectile2 
{
	radius 18
	height 10
	speed 20
	damage 3
	Scale 0.6
	Projectile
	States
	{
	Spawn:
		OCFX ABCDEFGHIJKLMNOPQR 4 bright
		Goto Death
	Death:
		OCFX ST 4 bright 
		Stop
	}
}

// Used by Cycloid Emperor
ACTOR DukeRocket2 
{
	radius 6
	height 4
	scale 0.5
	speed 20
	damage 10
	explosiondamage 16
	Explosionradius 64
	DeathSound "BOEXP"
	Projectile
	States
	{
	Spawn:
		DRPG A 4 
		Loop
	Death:
		DNEX A 3 Bright A_explode
		DNEX BCDEFGHIJKLMNOPQRSTU 3 bright 
		Stop
	}
}

// --->
// Invasion Spawn Spots
// --->
actor PigcopSpot : CustomMonsterInvasionSpot 10006
{DropItem Pigcop}

actor EnforcerSpot : CustomMonsterInvasionSpot 10003
{DropItem Enforcer}

actor OctopusSpot : CustomMonsterInvasionSpot 10005
{DropItem Octabrain}

actor AscoSpot : CustomMonsterInvasionSpot 10000
{DropItem AssaultCommander}

actor WarlordSpot : CustomMonsterInvasionSpot 10007
{DropItem Battlelord}

actor OverlordSpot : CustomMonsterInvasionSpot 10008
{DropItem Overlord}

actor CycloidEmperorSpot : CustomMonsterInvasionSpot 10009
{DropItem CycloidEmperor}
